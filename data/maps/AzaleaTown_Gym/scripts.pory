mapscripts AzaleaTown_Gym_MapScripts { }

script AzaleaTown_Gym_EventScript_Bugsy {
    trainerbattle_single(TRAINER_BUGSY, AzaleaTown_Gym_Text_BugsyPreBattle, AzaleaTown_Gym_Text_BugsyDefeat, AzaleaTown_Gym_EventScript_BugsyDefeated, NO_MUSIC)
    if (!flag(FLAG_MOVE_TUTOR_TAUGHT_FURY_CUTTER))
    {
        goto(AzaleaTown_Gym_EventScript_GiveSteelWing)
    }
    msgbox(AzaleaTown_Gym_Text_BugsyOutro)
	release
}

text AzaleaTown_Gym_Text_BugsyPreBattle {
	"I'm BUGSY! I never lose when it\n"
	"comes to BUG {POKEMON}.\p"
	"My research is going to make me the\n"
	"authority on BUG {POKEMON}!\p"
	"Let me demonstrate what I've learned\n"
	"from my studies."
}

text AzaleaTown_Gym_Text_BugsyDefeat {
	"Whoa, amazing!\n"
	"You're an expert on {POKEMON}!\l"
	"My research isn't complete yet.\p"
	"Okay, you win.\n"
	"Take this BADGE."
}

script AzaleaTown_Gym_EventScript_BugsyDefeated {
	textcolor(MSG_COLOR_SYS)
    message("{PLAYER} received the HIVEBADGE\n"
            "from BUGSY.")
    waitmessage
    call(Common_EventScript_PlayGymBadgeFanfare)
	textcolor(MSG_COLOR_PREV)
    setflag(FLAG_DEFEATED_AZALEA_GYM)
    setflag(FLAG_BADGE02_GET)
    setgymtrainers(2)
    goto(AzaleaTown_Gym_EventScript_GiveSteelWing)
}

script AzaleaTown_Gym_EventScript_GiveSteelWing {
    msgbox("Do you know the benefits of the\n"
		   "HIVEBADGE?\p"
		   "If you have it, {POKEMON} up to Lv.\n"
		   "30 will obey, even traded ones.\p"
		   "{POKEMON} that know CUT will be able\n"
		   "to use it outside of battle, too.\p"
		   "Here, I also want you to have this.\p"
           "…Huh? I thought I had a TM here…\n"
           "Nevermind, I'll teach it myself!\p"
           "Do you want me to teach one of your\n"
           "{POKEMON} FURY CUTTER?\p"
           "It's a move that, if you don't miss,\n"
           "It gets stronger every turn.\p"
           "Isn't that great?\n"
           "I discovered it!\p"
           "So, do you want me to show your\n"
           "{POKEMON} how to use it?", MSGBOX_YESNO)
    if(!var(VAR_RESULT))
    {
        goto(MoveTutor_EventScript_FuryCutterDeclined)
    }
    msgbox("Okay, which {POKEMON} should I tutor?", MSGBOX_DEFAULT)
    setvar(VAR_0x8005, TUTOR_MOVE_FURY_CUTTER)
	call(MoveTutor_EventScript_OpenPartyMenu)
    if(var(VAR_RESULT) == 0)
    {
        goto(MoveTutor_EventScript_FuryCutterDeclined)
    }
    setflag(FLAG_MOVE_TUTOR_TAUGHT_FURY_CUTTER)
    goto(AzaleaTown_Gym_EventScript_Bugsy)
}script(local) MoveTutor_EventScript_FuryCutterDeclined{
    msgbox(AzaleaTown_Gym_Text_BugsyOutro)
    release
}text(local) AzaleaTown_Gym_Text_BugsyOutro{
    "BUG {POKEMON} are deep. There are\nmany mysteries to be explored.\pStudy your favorites thoroughly."
}

script AzaleaTown_Gym_EventScript_GymGuide {
    lock
	faceplayer
    if (flag(FLAG_DEFEATED_AZALEA_GYM))
    {
        msgbox("Well done! That was a great clash\n"
               "of talented young TRAINERS.\p"
               "With people like you, the future of\n"
               "{POKEMON} is bright!")
    }
    else
    {
        msgbox("Yo, challenger!\p"
               "BUGSY's young, but his knowledge of\n"
               "BUG {POKEMON} is for real.\p"
               "It's going to be tough without my\n"
               "advice. Let's see…\p"
               "BUG {POKEMON} can't stand being too\n"
               "close to fire.\p"
               "FLYING-type moves are super-\n"
               "effective too.")
    }
	release
}

script AzaleaTown_Gym_EventScript_GymStatue {
    lockall
    if (flag(FLAG_BADGE02_GET))
    {
        msgbox("AZALEA TOWN {POKEMON} GYM\n"
               "LEADER: BUGSY\p"
               "WINNING TRAINERS:\n"
               "{PLAYER}")
    }
    else
    {
        msgbox("AZALEA TOWN {POKEMON} GYM")
    }
    releaseall
}

script AzaleaTown_Gym_EventScript_Benny {
	trainerbattle_single(TRAINER_BENNY, "BUG {POKEMON} evolve young, so they\nget stronger that much faster.", "Just evolving isn't enough!")
	msgbox("{POKEMON} become stronger if they\nevolve. Really!", MSGBOX_AUTOCLOSE)
}

script AzaleaTown_Gym_EventScript_Al {
	trainerbattle_single(TRAINER_AL, "BUG {POKEMON} are cool and tough!\nI'll prove it to you!", "You proved how tough you are…")
	msgbox("They're so cool, but most girls\ndon't like BUG {POKEMON}.\lI don't know why…", MSGBOX_AUTOCLOSE)
}

script AzaleaTown_Gym_EventScript_Josh {
	trainerbattle_single(TRAINER_JOSH, "You saved all the SLOWPOKE?\nWhew, you're mighty!\pBut my grown-up {POKEMON} are pretty\ntough too!", "Urrgggh!")
	msgbox("I guess I should teach them\nbetter moves…", MSGBOX_AUTOCLOSE)
}

script AzaleaTown_Gym_EventScript_Amy {
	trainerbattle_double(TRAINER_AMY_AND_MAY, "AMY: Hi! Are you challenging the\nLEADER? No way!", AzaleaTown_Gym_Text_AmyAndMayDefeat, "AMY: Your {POKEMON} doesn't have a\nfriend to battle with!")
	msgbox("AMY: You're really strong!", MSGBOX_AUTOCLOSE)
}text(local) AzaleaTown_Gym_Text_AmyAndMayDefeat{
    "AMY & MAY: Oh, double goodness!"
}

script AzaleaTown_Gym_EventScript_May {
	trainerbattle_double(TRAINER_AMY_AND_MAY, "MAY: You want to see the LEADER?\nWe come first!", AzaleaTown_Gym_Text_AmyAndMayDefeat, "MAY: Oh, you don't have another\n{POKEMON} with you? What a shame…")
	msgbox("MAY: Our BUG {POKEMON} lost!\nOh, what a shame.", MSGBOX_AUTOCLOSE)
}
