mapscripts VermilionCity_Port_MapScripts {
    MAP_SCRIPT_ON_TRANSITION{
        clearflag(FLAG_FAST_SHIP_HAS_ARRIVED)
        setflag(FLAG_VISITED_VERMILION_CITY)
        getplayerxy(VAR_0x8008, VAR_RESULT)
        if(var(VAR_RESULT) != 22)
        {
            setvar(VAR_TEMP_1, 1)
        }
        else
        {
            setflag(FLAG_TEMP_5)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_1, 0{
            lock
            applymovement(OBJ_EVENT_ID_PLAYER, OlivineCity_Port_Movement_PlayerExitsShip)
            waitmovement(0)
            addobject(3)
            applymovement(3, Common_Movement_WalkInPlaceFastestUp)
            playse(SE_EXIT)
            applymovement(OBJ_EVENT_ID_PLAYER, OlivineCity_Port_Movement_PlayerExitsDock)
            waitmovement(0)
            setvar(VAR_TEMP_1, 1)
            setrespawn(HEAL_LOCATION_VERMILION_CITY)
            release
            end
        }
    ]
}

script VermilionCity_Port_Eventscript_Trigger{
    if(flag(FLAG_TEMP_3) || flag(FLAG_TEMP_4))
    {
        setvar(VAR_TEMP_0, 1)
        release
        end
    }
    turnobject(1, DIR_EAST)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    setvar(VAR_LAST_TALKED, 1)
    textcolor(0)
    goto(VermilionCity_Port_Eventscript_Ferryman)
}

script VermilionCity_Port_Eventscript_Ferryman{
    if(flag(FLAG_TEMP_3)) // Already rode
    {
        msgbox(OlivineCity_Port_Text_CantBoardNow, MSGBOX_NPC)
        end
    }
    lock
    getplayerxy(VAR_0x8008, VAR_RESULT)
    if(var(VAR_0x8008) < 7)
    {
        faceplayer
    }
    gettime
    switch(var(VAR_0x8003))
    {
        case DAY_MONDAY:
        case DAY_TUESDAY:
            msgbox("The FAST SHIP will sail on\nWEDNESDAY.", MSGBOX_DEFAULT)
            closemessage
            getplayerxy(VAR_0x8008, VAR_RESULT)
            if(var(VAR_0x8008) == 7 && var(VAR_RESULT) == 15)
            {
                applymovement(OBJ_EVENT_ID_PLAYER, OlivineCity_Port_Movement_PlayerStepsAway)
                waitmovement(0)
            }
            release
            end
        case DAY_THURSDAY:
        case DAY_FRIDAY:
        case DAY_SATURDAY:
            msgbox("The FAST SHIP will sail\nnext SUNDAY." , MSGBOX_DEFAULT)
            closemessage
            getplayerxy(VAR_0x8008, VAR_RESULT)
            if(var(VAR_0x8008) == 7 && var(VAR_RESULT) == 15)
            {
                applymovement(OBJ_EVENT_ID_PLAYER, OlivineCity_Port_Movement_PlayerStepsAway)
                waitmovement(0)
            }
            release
            end
    }
    msgbox(OlivineCity_Port_Text_Welcome, MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO)
    {
        msgbox(OlivineCity_Port_Text_WeHopeToSeeYouAgain, MSGBOX_DEFAULT)
        closemessage
        getplayerxy(VAR_0x8008, VAR_RESULT)
        if(var(VAR_0x8008) == 7 && var(VAR_RESULT) == 15)
        {
            applymovement(OBJ_EVENT_ID_PLAYER, OlivineCity_Port_Movement_PlayerStepsAway)
            waitmovement(0)
        }
        release
        end
    }
    msgbox(OlivineCity_Port_Text_AskForTicket, MSGBOX_DEFAULT)
    checkitem(ITEM_SS_TICKET, 1)
    if(var(VAR_RESULT) == FALSE)
    {
        msgbox(OlivineCity_Port_Text_NoTicket, MSGBOX_DEFAULT)
        closemessage
        getplayerxy(VAR_0x8008, VAR_RESULT)
        if(var(VAR_0x8008) == 7 && var(VAR_RESULT) == 15)
        {
            applymovement(OBJ_EVENT_ID_PLAYER, OlivineCity_Port_Movement_PlayerStepsAway)
            waitmovement(0)
        }
        release
        end
    }
    msgbox(OlivineCity_Port_Text_FlashTicket, MSGBOX_DEFAULT)
    closemessage
    setflag(FLAG_TEMP_4) // Used in pokecrystal to not play a script
    getplayerxy(VAR_0x8008, VAR_RESULT)
    if(var(VAR_RESULT) == 14)
    {
        applymovement(OBJ_EVENT_ID_PLAYER, OlivineCity_Port_Movement_PlayerToTriggerTop)
        waitmovement(0)
    }
    elif(var(VAR_0x8008) == 5)
    {
        applymovement(OBJ_EVENT_ID_PLAYER, OlivineCity_Port_Movement_PlayerToTriggerLeft)
        waitmovement(0)
    }
    applymovement(OBJ_EVENT_ID_PLAYER, OlivineCity_Port_Movement_PlayerToShip)
    waitmovement(0)
    goto(VermilionCity_Port_Eventscript_GetOnBoard)
}

script VermilionCity_Port_Eventscript_GetOnBoard{
    setvar(VAR_LAST_TALKED, 3)
    lock
    textcolor(0)
    if(flag(FLAG_TEMP_3)) // Already rode
    {
        msgbox(OlivineCity_Port_Text_CantBoardNow, MSGBOX_NPC)
        end
    }
    msgbox(OlivineCity_Port_Text_DepartingSoon, MSGBOX_DEFAULT)
    closemessage
    turnobject(3, DIR_SOUTH)
    delay(10)
    playse(SE_EXIT)
    setflag(FLAG_TEMP_5)
    removeobject(3)
    waitse
    applymovement(OBJ_EVENT_ID_PLAYER, OlivineCity_Port_Movement_PlayerEntersShip)
    waitmovement(0)
    // Trainers on the ship can be rebattled
    // This clears the flags of the Vermilion -> Olivine trainers
    cleartrainerflag(TRAINER_COREY)
    cleartrainerflag(TRAINER_ETHAN)
    cleartrainerflag(TRAINER_KEN)
    cleartrainerflag(TRAINER_CLYDE)
    cleartrainerflag(TRAINER_GEORGIA)
    cleartrainerflag(TRAINER_JEREMY)
    cleartrainerflag(TRAINER_KENNETH)
    cleartrainerflag(TRAINER_SHIRLEY)
    cleartrainerflag(TRAINER_NATE)
    cleartrainerflag(TRAINER_RICKY)
    setflag(FLAG_FAST_SHIP_DESTINATION_OLIVINE)
    setdynamicwarp(MAP_VERMILION_CITY_PORT, 1, 0, 0)
    warp(MAP_FAST_SHIP_1F, 0, 0, 0)
    waitstate
}

script VermilionCity_Port_EventScript_Boy{
    msgbox("You came from JOHTO?\pI hear many rare {POKEMON} live\nover there.", MSGBOX_NPC)
    end
}