mapscripts EcruteakCity_DanceTheater_MapScripts {
    // Manually overwrite the palSlots with Kimono Girl's palettes
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_TEMP_0, 0{
            callnative(PatchKimonoGirlPalettesToSlots)
        }
    ]
    MAP_SCRIPT_ON_RETURN_TO_FIELD
    {
        if(flag(FLAG_TEMP_1))
        {
            callnative(OverrideKimonoGirlsPaletteSlots)
            callnative(PatchKimonoGirlPalettesToSlots)
        }
    }
}

// The Kimono Girl sprite is only one graphicsId so, to show the different palettes,
// each Kimono Girl object event needs to have the palette slot its using overridden.
// This way, we don't have to waste 4 EVENT_OBJ_GFX_IDs on their different colors.
// The Dance Theater never has its event objects culled when offscreen to faciliate this.
script EcruteakCity_DanceTheater_EventScript_OverrideKimonoGirlsPalSlots{
    setflag(FLAG_TEMP_1)
    callnative(OverrideKimonoGirlsPaletteSlots)
    callnative(PatchKimonoGirlPalettesToSlots)
    setvar(VAR_TEMP_1, 0)
    release
}

script EcruteakCity_DanceTheater_Eventscript_PanelSign {
    msgbox("It's a fancy panel that's decorated\nwith flowers.", MSGBOX_SIGN)
    end
}

script EcruteakCity_DanceTheater_Eventscript_OldLady{
    msgbox("The KIMONO GIRLS are so beautiful…\nBut they have to go through\lrigorous training.\pAnd they have to learn to follow\ncustoms before appearing in public.\pBut if you love something, anything\nis possible.", MSGBOX_NPC)
    end
}

script EcruteakCity_DanceTheater_Eventscript_Boy{
    msgbox("That man's always with his RHYDON.\nSays he wants a {POKEMON} that can\lSURF and dance.\pIs he trying to make a synchronized\nswimming {POKEMON}?", MSGBOX_NPC)
    end
}

script EcruteakCity_DanceTheater_Eventscript_Rhydon{
    lock
    faceplayer
    playmoncry(SPECIES_RHYDON, 0)
    msgbox("RHYDON: Gugooh gugogooh!", MSGBOX_DEFAULT)
    waitmoncry
    release
    end
}

script EcruteakCity_DanceTheater_Eventscript_Gentleman{
    lock
    faceplayer
    message("Not only are the KIMONO GIRLS great\ndancers, they're also skilled at\l{POKEMON}.\pI always challenge them, but I've\nnever even left a scratch…\p")
    waitmessage
    checkitem(ITEM_HM03, 1) // checking item to save a flag; HM03 cannot be discarded without cheating.
    copyvar(VAR_0x8008, VAR_RESULT)
    checkpcitem(ITEM_HM03, 1) // ordinarily cannot be deposited, but might as well check
    if(var(VAR_0x8008) == TRUE || var(VAR_RESULT) == TRUE)
    {
        msgbox("I wish my {POKEMON} were as elegant\nas the KIMONO GIRLS…", MSGBOX_NPC)
        end
    }
    if(!(defeated(TRAINER_NAOKO) && defeated(TRAINER_SAYO) && defeated(TRAINER_ZUKI) && defeated(TRAINER_KUNI) && defeated(TRAINER_MIKI)))
    {
        checkplayergender
        if(var(VAR_RESULT) == MALE)
        {
            bufferstring(0, "Lad")
        }
        else
        {
            bufferstring(1, "Lassie")
        }
        msgbox("{STR_VAR_1}, if you can defeat all the\nKIMONO GIRLS, I'll give you a gift.", MSGBOX_NPC)
        end
    }
    msgbox("The way you battled, it was like\nwatching a dance.\pIt was a rare treat to see!\pI want you to have this.\nDon't worry--take it!", MSGBOX_DEFAULT)
    giveitem(ITEM_HM03)
    msgbox("That's SURF. It's a move that lets\n{POKEMON} swim across water.", MSGBOX_NPC)
    end
}
// Kimono Girls from left to right
script EcruteakCity_DanceTheater_Eventscript_KimonoGirlNaoko{ // Flareon
    trainerbattle_single(TRAINER_NAOKO, "You have lovely {POKEMON}.\nMay I see them in battle?", "Oh, you are very strong.")
    msgbox("I enjoyed that bout.\nI would like to see you again.", MSGBOX_NPC)
    end
}

script EcruteakCity_DanceTheater_Eventscript_KimonoGirlSayo{ // Espeon
    trainerbattle_single(TRAINER_SAYO, "I always dance with my {POKEMON}.\nOf course, I also train them.", "Oh, so close!\nI almost had you.")
    msgbox("Rhythm is important for both dancing\nand {POKEMON}.", MSGBOX_NPC)
    end
}

script EcruteakCity_DanceTheater_Eventscript_KimonoGirlZuki{ // Umbreon
    trainerbattle_single(TRAINER_ZUKI, "Isn't my barrette pretty?\nOh. A {POKEMON} battle?", "I don't have any {POKEMON} left…")
    msgbox("I put a different flower in my\nbarrette every month.", MSGBOX_NPC)
    end
}

script EcruteakCity_DanceTheater_Eventscript_KimonoGirlKuni{ // Vaporeon
    trainerbattle_single(TRAINER_KUNI, "Oh, you're a cute TRAINER.\nWould you like to battle?", "You're stronger than you look.")
    msgbox("I trained a lot, so I thought I was\na capable TRAINER. I guess I'm not.", MSGBOX_NPC)
    end
}

script EcruteakCity_DanceTheater_Eventscript_KimonoGirlMiki{ // Jolteon
    trainerbattle_single(TRAINER_MIKI, "Do you like my dancing?\nI'm good at {POKEMON} too.", "Ooh, you're good at {POKEMON} too.")
    msgbox("I can keep dancing because there are\npeople who enjoy what I do.\pMy {POKEMON} keep my spirits up too.", MSGBOX_NPC)
    end
}
