const OBJ_RIVAL = 9

mapscripts AzaleaTown_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_VISITED_AZALEA_TOWN)
        checkitem(ITEM_GS_BALL, 1)
        if(!(!flag(FLAG_HIDE_AZALEA_TOWN_KURT) && var(VAR_RESULT) == FALSE))
        {
            setvar(VAR_TEMP_3, 1)
        }
        if(flag(FLAG_VISITED_VERMILION_CITY))
        {
            setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SLOWPOKE)
        }
        else
        {
            setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SLOWPOKE_TAILLESS)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_2, 0: AzaleaTown_OnFrame
    ]
}

script AzaleaTown_OnFrame{
    getplayerxy(VAR_RESULT, VAR_0x8008)
    if(!flag(FLAG_HIDE_AZALEA_TOWN_KURT) && var(VAR_RESULT) != 16 && var(VAR_0x8008) != 6)
    {
        setvar(VAR_TEMP_2, 1)
        end
    }
    checkitem(ITEM_GS_BALL, 1)
    if(!flag(FLAG_HIDE_AZALEA_TOWN_KURT) && var(VAR_RESULT) == FALSE)
    {
        call(AzaleaTown_EventScript_KurtForestRestless)
        setvar(VAR_TEMP_2, 1)
        setvar(VAR_TEMP_3, 1)
        release
        end
    }
    setvar(VAR_TEMP_2, 1)
    end
}movement(local) AzaleaTown_Movement_KurtsHouseToKurt{
    walk_left * 2
    walk_up * 3
    walk_left
}script AzaleaTown_EventScript_KurtForestRestless{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, AzaleaTown_Movement_KurtsHouseToKurt)
    waitmovement(0)
    bufferitemname(0, ITEM_GS_BALL)
    textcolor(MSG_COLOR_BLUE)
    msgbox("ILEX FOREST is restless!\pWhat is going on?")
    turnobject(11, DIR_EAST)
    msgbox("{PLAYER}, here's your {STR_VAR_1} back!\pCould you go see why ILEX FOREST\nis so restless?")
    giveitem(ITEM_GS_BALL, 1)
    turnobject(11, DIR_WEST)
    clearflag(FLAG_HIDE_ILEX_FOREST_LASS)
    return
}

script AzaleaTown_EventScript_KurtsHouseTrigger {
    checkitem(ITEM_GS_BALL, 1)
    if(!flag(FLAG_HIDE_AZALEA_TOWN_KURT) && var(VAR_RESULT) == FALSE)
    {
        call(AzaleaTown_EventScript_KurtForestRestless)
        setvar(VAR_TEMP_3, 1)
        release
        end
    }
}

script AzaleaTown_EventScript_RivalTrigger_1 {
    lockall
    setvar(VAR_TEMP_1, 0)
    setobjectxyperm(OBJ_RIVAL, 19, 10)
    goto(AzaleaTown_EventScript_RivalTrigger_All)
}

script AzaleaTown_EventScript_RivalTrigger_2 {
    lockall
    setvar(VAR_TEMP_1, 1)
    goto(AzaleaTown_EventScript_RivalTrigger_All)
}

script AzaleaTown_EventScript_RivalTrigger_3 {
    lockall
    setvar(VAR_TEMP_1, 2)
    setobjectxyperm(OBJ_RIVAL, 19, 10)
    goto(AzaleaTown_EventScript_RivalTrigger_All)
}

script AzaleaTown_EventScript_RivalTrigger_4 {
    lockall
    setvar(VAR_TEMP_1, 3)
    goto(AzaleaTown_EventScript_RivalTrigger_All)
}

script AzaleaTown_EventScript_RivalTrigger_All {
    addobject(OBJ_RIVAL)
	fadenewbgm(0)
    textcolor(MSG_COLOR_BLUE)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFastestRight)
	waitmovement(0)
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
	waitmovement(0)

    if (var(VAR_TEMP_1) <= 1)
    {
        applymovement(OBJ_EVENT_ID_PLAYER, AzaleaTown_Movement_PlayerWatchRivalDown)
        applymovement(OBJ_RIVAL, AzaleaTown_Movement_RivalToPlayerUp)
        waitmovement(0)
    }
    else
    {
        applymovement(OBJ_EVENT_ID_PLAYER, AzaleaTown_Movement_PlayerWatchRivalUp)
        applymovement(OBJ_RIVAL, AzaleaTown_Movement_RivalToPlayerDown)
        waitmovement(0)
    }

    playbgm(MUS_ENCOUNTER_RIVAL, 0)
	savebgm(MUS_RIVAL_EXIT)
    msgbox("…Tell me something.\p"
           "Is it true that TEAM ROCKET has\n"
           "returned?\p"
           "What? You beat them?\n"
           "Hah! Quit lying.\p"
           "You're not joking?\n"
           "Then let's see how good you are.")

    setvar(VAR_LAST_TALKED, OBJ_RIVAL)

    if (var(VAR_STARTER_MON) == STARTER_CHIKORITA)
    {
        trainerbattle_wintext(TRAINER_RIVAL_AZALEA_CHIKORITA, 0, AzaleaTown_Text_RivalLost, AzaleaTown_Text_RivalWon)
    }
    elif (var(VAR_STARTER_MON) == STARTER_TOTODILE)
    {
        trainerbattle_wintext(TRAINER_RIVAL_AZALEA_TOTODILE, 0, AzaleaTown_Text_RivalLost, AzaleaTown_Text_RivalWon)
    }
    elif (var(VAR_STARTER_MON) == STARTER_CYNDAQUIL)
    {
        trainerbattle_wintext(TRAINER_RIVAL_AZALEA_CYNDAQUIL, 0, AzaleaTown_Text_RivalLost, AzaleaTown_Text_RivalWon)
    }

    msgbox("I hate the weak.\p"
           "{POKEMON}, TRAINERS.\n"
           "It doesn't matter who or what.\p"
           "I'm going to be strong and wipe\n"
           "out the weak.\p"
           "That goes for TEAM ROCKET too.\p"
           "They act big and tough together, but\n"
           "get them alone and they're weak.\p"
           "I hate them all.\p"
           "You stay out of my way. A weakling\n"
           "like you is only a distraction.")
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFastestLeft)
    if (var(VAR_TEMP_1) == 0 || var(VAR_TEMP_1) == 2)
    {
        applymovement(OBJ_RIVAL, AzaleaTown_Movement_RivalExitUpper)
        waitmovement(0)
    }
    else
    {
        applymovement(OBJ_RIVAL, AzaleaTown_Movement_RivalExitLower)
        waitmovement(0)
    }

    playse(SE_EXIT)
    removeobject(OBJ_RIVAL)
    waitse
    savebgm(MUS_DUMMY)
	fadedefaultbgm
    setvar(VAR_AZALEA_TOWN_STATE, 2)
    releaseall
}

text AzaleaTown_Text_RivalLost {
    "… Humph!\n"
    "Useless {POKEMON}!\p"
    "Listen, you. You only won because\n"
    "my {POKEMON} were weak."
}

text AzaleaTown_Text_RivalWon {
    "…Humph!\n"
    "I knew you were lying."
}

movement AzaleaTown_Movement_PlayerWatchRivalDown {
    delay_16 * 6
    walk_in_place_fastest_down
}

movement AzaleaTown_Movement_PlayerWatchRivalUp {
    delay_16 * 6
    walk_in_place_fastest_up
}

movement AzaleaTown_Movement_RivalToPlayerUp {
    walk_left * 8
    walk_in_place_fastest_up
}

movement AzaleaTown_Movement_RivalToPlayerDown {
    walk_left * 8
    walk_in_place_fastest_down
}

movement AzaleaTown_Movement_RivalExitUpper {
    walk_left
    walk_down
    walk_left * 2
}

movement AzaleaTown_Movement_RivalExitLower {
    walk_left * 3
}

script AzaleaTown_EventScript_SlowpokeWell_B1FGuard {
    msgbox("It's unsafe to go in there, so I'm\n"
           "standing guard.\p"
           "Aren't I a good Samaritan?", MSGBOX_NPC)
}

script AzaleaTown_EventScript_OldMan {
    lock
    faceplayer
    if (flag(FLAG_CLEARED_SLOWPOKE_WELL))
    {
        msgbox("The SLOWPOKE have returned.\p"
               "Knowing them, they could've just\n"
               "been goofing off somewhere.")
    }
    else
    {
        msgbox("The SLOWPOKE have disappeared\n"
               "from town…\p"
               "I heard their TAILS are being\n"
               "sold somewhere.")
    }
    release
}

script AzaleaTown_EventScript_Woman {
    msgbox("Did you come to get KURT to make\n"
           "some BALLS?\p"
           "A lot of people travel to AZALEA\n"
           "to do just that.", MSGBOX_NPC)
}

script AzaleaTown_EventScript_Youngster {
    msgbox("Cut through AZALEA and you'll be\n"
           "in ILEX FOREST.\p"
           "But these skinny trees make it\n"
           "impossible to get through.\p"
           "The CHARCOAL MAN's {POKEMON} can CUT\n"
           "down trees.", MSGBOX_NPC)
}

script AzaleaTown_EventScript_Slowpoke {
    lock
    faceplayer
    message("SLOWPOKE: …\n"
           "…… …… ……")
    waitmessage
    delay(60)
    waitse
    playmoncry(SPECIES_SLOWPOKE, 0)
    msgbox("…… ……Yawn?")
    waitmoncry
    release
}

script AzaleaTown_EventScript_GymRocket {
    msgbox("Do you know about SLOWPOKETAIL?\n"
           "I heard it's tasty!\p"
           "Aren't you glad I told you that?", MSGBOX_NPC)
}

script AzaleaTown_EventScript_TownSign {
    msgbox("AZALEA TOWN\n"
           "Where People and {POKEMON} Live in\l"
           "Happy Harmony", MSGBOX_SIGN)
}

script AzaleaTown_EventScript_KurtsHouseSign {
    msgbox("KURT'S HOUSE", MSGBOX_SIGN)
}

script AzaleaTown_EventScript_GymSign {
    msgbox("AZALEA TOWN {POKEMON} GYM\n"
           "LEADER: BUGSY\p"
           "The Walking BUG {POKEMON}\n"
           "Encyclopedia", MSGBOX_SIGN)
}

script AzaleaTown_EventScript_SlowpokeWell_B1FSign {
    msgbox("SLOWPOKE WELL\n"
           "Also known as the RAINMAKER WELL.\p"
           "Locals believe that a SLOWPOKE's\n"
           "yawn summons rain.\p"
           "Records show that a SLOWPOKE's yawn\n"
           "ended a drought 400 years ago.", MSGBOX_SIGN)
}

script AzaleaTown_EventScript_CharcoalKilnSign {
    msgbox("CHARCOAL KILN", MSGBOX_SIGN)
}

script AzaleaTown_EventScript_IlexForestSign {
    msgbox("ILEX FOREST\n"
           "Enter through the gate.", MSGBOX_SIGN)
}

script AzaleaTown_EventScript_Kurt{
   msgbox("Could you go see why ILEX FOREST\nis so restless?", MSGBOX_NPC)
   turnobject(11, DIR_WEST)
   end
}