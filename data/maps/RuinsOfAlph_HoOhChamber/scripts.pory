mapscripts RuinsOfAlph_HoOhChamber_MapScripts {
    MAP_SCRIPT_ON_RETURN_TO_FIELD: RuinsOfAlph_HoOhChamber_MapScript_OpenHoleInWall // could get a Water Stone from party held items
    MAP_SCRIPT_ON_TRANSITION: RuinsOfAlph_HoOhChamber_MapScript_OpenHoleInWall
    MAP_SCRIPT_ON_LOAD {
        if (flag(FLAG_SOLVED_HOOH_PUZZLE))
        {
            setmetatile(4, 5, METATILE_RuinsOfAlph_Puzzle_Hole_Left, FALSE)
            setmetatile(5, 5, METATILE_RuinsOfAlph_Puzzle_Hole_Center, FALSE)
            setmetatile(6, 5, METATILE_RuinsOfAlph_Puzzle_Hole_Right, FALSE)
        }
        if (flag(FLAG_OPENED_WALL_IN_HO_OH_CHAMBER))
        {
            setmetatile(5, 1, METATILE_RuinsOfAlph_HoleInWall, FALSE)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_0, 1 {
            setflag(FLAG_HIDE_MAP_NAME_POPUP)
            lockall
            setvar(VAR_0x8004, 1)  # vertical pan
            setvar(VAR_0x8005, 1)  # horizontal pan
            setvar(VAR_0x8006, 20) # num shakes
            setvar(VAR_0x8007, 3)  # shake delay
            special(ShakeCamera)
            waitstate
            playse(SE_PIN)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
            waitmovement(0)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
            waitmovement(0)
            delay(4)
            setvar(VAR_0x8004, 2)  # vertical pan
            setvar(VAR_0x8005, 2)  # horizontal pan
            setvar(VAR_0x8006, 20) # num shakes
            setvar(VAR_0x8007, 3)  # shake delay
            special(ShakeCamera)
            delay(30)
            setmetatile(5, 1, METATILE_RuinsOfAlph_HoleInWall, FALSE)
            special(DrawWholeMapView)
            playse(SE_M_ROCK_THROW)
            waitstate
            clearflag(FLAG_HIDE_MAP_NAME_POPUP)
            setvar(VAR_TEMP_0, 0)
            setflag(FLAG_OPENED_WALL_IN_HO_OH_CHAMBER)
            releaseall
        }
    ]
}script RuinsOfAlph_HoOhChamber_MapScript_OpenHoleInWall{ //interferes with solving HoOh Puzzle
    if(!flag(FLAG_TEMP_1))
    {
        if(!flag(FLAG_OPENED_WALL_IN_HO_OH_CHAMBER))
        {
            callnative(IsHoOhInFirstInParty)
            if(var(VAR_RESULT))
            {
                setvar(VAR_TEMP_0, 1)
            }
        }
    }
}

script RuinsOfAlph_HoOhChamber_EventScript_HoOhDescription {
    msgbox("A {POKEMON} that flew gracefully on\nrainbow-colored wings.", MSGBOX_SIGN)
}

script RuinsOfAlph_HoOhChamber_EventScript_BackWall {
    lockall
    if (flag(FLAG_OPENED_WALL_IN_HO_OH_CHAMBER))
    {
        msgbox("There's a big hole in the wall!", MSGBOX_SIGN)
    }
    else
    {
        message("Patterns appeared on the wallsâ€¦")
        waitmessage
        unownmessage(RuinsOfAlph_HoOhChamber_Unown_Light)
        waitbuttonpress
        closeunownmessage
    }
    releaseall
}

raw `
RuinsOfAlph_HoOhChamber_Unown_Light:
.unown "HO OH$"
`

script RuinsOfAlph_HoOhChamber_EventScript_HoOhPuzzle {
    lockall
    setflag(FLAG_TEMP_1)
    setvar(VAR_0x8004, 3)
    special(DoRuinsOfAlphPuzzle)
    waitstate
    if (var(VAR_RESULT) == 1)
    {
        setflag(FLAG_SOLVED_HOOH_PUZZLE)
        call(RuinsOfAlph_Chambers_EventScript_SolvePuzzle)
        setwarp(MAP_RUINS_OF_ALPH_INNER_CHAMBER, 5, 0, 0)
        special(DoFallWarp)
        waitstate
    }
    clearflag(FLAG_TEMP_1)
    releaseall
}