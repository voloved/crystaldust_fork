mapscripts TinTower_Roof_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setescapewarp(MAP_ECRUTEAK_CITY, 255, 41, 11)
        if(flag(FLAG_DEFEATED_HO_OH))
        {
            setvar(VAR_TEMP_1, 1)
            setflag(FLAG_TEMP_1)
        }
    }
    MAP_SCRIPT_ON_RESUME {
        if (flag(FLAG_SYS_CTRL_OBJ_DELETE))
        {
            specialvar(VAR_RESULT, GetBattleOutcome)
            if (var(VAR_RESULT) == B_OUTCOME_CAUGHT)
            {
                setflag(FLAG_TEMP_1)
                removeobject(VAR_LAST_TALKED)
            }
        }
    }
}

script TinTower_Roof_EventScript_Trigger {
    lockall
    setvar(VAR_TEMP_1, 1)
    special(SpawnCameraObject)
    setvar(VAR_0x8004, 3)
    setvar(VAR_0x8005, 35)
    special(LoopWingFlapSE)
    applymovement(1, TinTower_Roof_Movement_HoOhAppear)
    applymovement(OBJ_EVENT_ID_CAMERA, TinTower_Roof_Movement_CameraPanUp)
    waitmovement(0)
    delay(50)
    playmoncry(SPECIES_HO_OH, 2)
    message("Shaoooh!")
    waitmessage
    delay(30)
    waitmoncry
    closemessage
    delay(60)
    setvar(VAR_0x8004, 3)
    setvar(VAR_0x8005, 35)
    special(LoopWingFlapSE)
    applymovement(OBJ_EVENT_ID_CAMERA, TinTower_Roof_Movement_CameraPanDown)
	applymovement(1, TinTower_Roof_Movement_HoOhApproach)
	waitmovement(0)
    special(RemoveCameraObject)
    setvar(VAR_0x8004, SPECIES_HO_OH)
	setvar(VAR_0x8005, 60)
	setvar(VAR_0x8006, ITEM_SACRED_ASH)
    setvar(VAR_LAST_TALKED, 1)
    special(CreateEventLegalEnemyMon)
	setflag(FLAG_SYS_CTRL_OBJ_DELETE)
	special(BattleSetup_StartLegendaryBattle)
	waitstate
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    setflag(FLAG_DEFEATED_HO_OH)
    specialvar(VAR_RESULT, GetBattleOutcome)
    switch (var(VAR_RESULT))
    {
        case B_OUTCOME_CAUGHT:
            setflag(FLAG_CAUGHT_HO_OH)
        case B_OUTCOME_WON:
        case B_OUTCOME_RAN:
        case B_OUTCOME_PLAYER_TELEPORTED:
            call(Common_EventScript_LegendaryFlewAway)
    }
    setflag(FLAG_EUSINE_LEFT_CELADON)
    releaseall
    end
}movement(local) TinTower_Roof_Movement_HoOhAppear{
    delay_16 * 2
    walk_in_place_down * 4
}movement(local) TinTower_Roof_Movement_CameraPanUp{
    walk_up * 3
}movement(local) TinTower_Roof_Movement_CameraPanDown{
    delay_16 * 2
    walk_down * 3
}movement(local) TinTower_Roof_Movement_HoOhApproach{
    walk_down * 5
    walk_in_place_down
    delay_16 * 2
}
