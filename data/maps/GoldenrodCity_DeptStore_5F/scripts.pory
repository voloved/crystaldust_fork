mapscripts GoldenrodCity_DeptStore_5F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        gettime
        if (var(VAR_0x8003) == DAY_SUNDAY)
        {
            clearflag(FLAG_HIDE_GOLDENROD_DEPT_STORE_RETURN_FRUSTRATION_LADY)
        }
        else
        {
            setflag(FLAG_HIDE_GOLDENROD_DEPT_STORE_RETURN_FRUSTRATION_LADY)
        }
    }
}

script GoldenrodCity_DeptStore_5F_EventScript_Clerk {
    lock
    faceplayer
    message(gText_HowMayIHelpYou)
    waitmessage
    pokemart(GoldenrodCity_DeptStore_5F_Pokemart_Clerk)
    msgbox(gText_PleaseComeAgain)
    release
}

raw `
.align 2
GoldenrodCity_DeptStore_5F_Pokemart_Clerk:
	.2byte ITEM_TM03_WATER_PULSE
	.2byte ITEM_TM33_REFLECT
	.2byte ITEM_TM16_LIGHT_SCREEN
	.2byte ITEM_NONE
`

script GoldenrodCity_DeptStore_5F_EventScript_ReturnFrustrationLady {
    lock
    faceplayer
    gettime
    if (var(VAR_0x8003) == DAY_SUNDAY &&
        !flag(FLAG_DAILY_GOLDENROD_DEPT_STORE_RETURN_FRUSTRATION_EVENT))
    {
        msgbox("Hello.\n"
               "Oh, your {POKEMON}…")
        specialvar(VAR_RESULT, GetLeadMonFriendshipScore)
        if (var(VAR_RESULT) >= 4)
        {
            msgbox("It's very attached to you.\p"
                   "This move should be perfect for\n"
                   "a pair like you.")
            checkitemspace(ITEM_TM27_RETURN, 1)
            if (!var(VAR_RESULT))
            {
                goto(Common_EventScript_ShowBagIsFull)
            }
            else
            {
                givetmhm_msg("{PLAYER} received TM27\n"
                            "from the woman.", ITEM_TM27_RETURN)
                setflag(FLAG_DAILY_GOLDENROD_DEPT_STORE_RETURN_FRUSTRATION_EVENT)
            }
        }
        elif (var(VAR_RESULT) >= 2)
        {
            msgbox("It's adorable!\n"
                   "You should teach it good TM moves.")
        }
        else
        {
            msgbox("It looks evil.\n"
                   "How about this TM for it?")
            checkitemspace(ITEM_TM21_FRUSTRATION, 1)
            if (!var(VAR_RESULT))
            {
                goto(Common_EventScript_ShowBagIsFull)
            }
            else
            {
                givetmhm_msg("{PLAYER} received TM21\n"
                            "from the woman.", ITEM_TM21_FRUSTRATION)
                setflag(FLAG_DAILY_GOLDENROD_DEPT_STORE_RETURN_FRUSTRATION_EVENT)
            }
        }
    }
    else
    {
        msgbox("There are sure to be TMs that are\n"
               "just perfect for your {POKEMON}.")
    }
    release
}

script GoldenrodCity_DeptStore_5F_EventScript_Mike {
    lock
    faceplayer
    setvar(VAR_0x8008, INGAME_TRADE_MACHOP)
    copyvar(VAR_0x8004, VAR_0x8008)
    specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
    copyvar(VAR_0x8009, VAR_RESULT)

    if (!flag(FLAG_GOLDENROD_NPC_TRADE_COMPLETED))
    {
        msgbox("I collect {POKEMON}.\n"
               "Do you have {STR_VAR_1}?\p"
               "Want to trade it for my {STR_VAR_2}?", MSGBOX_YESNO)
        
        if (var(VAR_RESULT) != NO)
        {
            special(ChoosePartyMon)
            waitstate
            lock
            faceplayer
            copyvar(VAR_0x800A, VAR_0x8004)
            
            if (var(VAR_0x8004) != 0xFF)
            {
                copyvar(VAR_0x8005, VAR_0x800A)
                specialvar(VAR_RESULT, GetTradeSpecies)
                copyvar(VAR_0x800B, VAR_RESULT)

                if (var(VAR_RESULT) == VAR_0x8009)
                {
                    copyvar(VAR_0x8004, VAR_0x8008)
                    copyvar(VAR_0x8005, VAR_0x800A)
                    special(CreateInGameTradePokemon)
                    special(DoInGameTradeScene)
                    waitstate
                    lock
                    faceplayer
                    bufferspeciesname(0, VAR_0x8009)
                    msgbox("Yay! I got myself {STR_VAR_1}!\n"
                           "Thanks!")
                    setflag(FLAG_GOLDENROD_NPC_TRADE_COMPLETED)
                }
                else
                {
                    bufferspeciesname(0, VAR_0x8009)
                    msgbox("Huh? That's not {STR_VAR_1}.\n"
                           "What a letdown…")
                }
            }
            else
            {
                msgbox(GoldenrodCity_DeptStore_5F_Text_Mike_NoTrade)
            }
        }
        else
        {
            msgbox(GoldenrodCity_DeptStore_5F_Text_Mike_NoTrade)
        }
    }
    else
    {
        bufferspeciesname(0, VAR_0x8009)
        msgbox("Hi, how's my old {STR_VAR_2} doing?")
    }

    release
}

text GoldenrodCity_DeptStore_5F_Text_Mike_NoTrade {
    "You don't want to trade?\n"
    "Aww…"
}

script GoldenrodCity_DeptStore_5F_EventScript_Lass {
    msgbox("On SUNDAYS, a lady comes to check\n"
           "out {POKEMON}.\p"
           "She even gives away TMs!", MSGBOX_NPC)
}

script GoldenrodCity_DeptStore_5F_EventScript_RenameGuy {
    msgbox("You can't rename a {POKEMON} you get\n"
           "in a trade.\p"
           "The name is a reflection of the\n"
           "original TRAINER's feelings for it.", MSGBOX_NPC)
}

script GoldenrodCity_DeptStore_5F_EventScript_MysteryGiftGirl {
    msgbox("MYSTERY GIFT.\p"
           "With just a little beep, you get\n"
           "a gift.\p"
           "Use it to “LINK TOGETHER WITH ALL!”", MSGBOX_NPC)
}

script GoldenrodCity_DeptStore_5F_EventScript_FloorNumber {
    msgbox("Customize Your {POKEMON}\p"
           "5F TM CORNER", MSGBOX_SIGN)
}
