mapscripts GoldenrodCity_Underground_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		if (!flag(FLAG_GOLDENROD_CITY_UNDERGROUND_DOOR_UNLOCKED))
		{
			setmetatile(24, 8, METATILE_Underground_Door_Closed, TRUE)
		}
	}
	MAP_SCRIPT_ON_TRANSITION {
		gettime
		switch (var(VAR_0x8003))
		{
		default:
			// DAY_SUNDAY
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_BARGAIN_MAN)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_OLDER_HAIRCUT_BROTHER)
			clearflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_YOUNGER_HAIRCUT_BROTHER)
			clearflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_HERB_WOMAN)
		case DAY_MONDAY:
			if (var(VAR_0x8002) != TIME_MORNING)
			{
				setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_BARGAIN_MAN)
			}
			else
			{
				clearflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_BARGAIN_MAN)
			}
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_OLDER_HAIRCUT_BROTHER)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_YOUNGER_HAIRCUT_BROTHER)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_HERB_WOMAN)
		case DAY_TUESDAY:
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_BARGAIN_MAN)
			clearflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_OLDER_HAIRCUT_BROTHER)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_YOUNGER_HAIRCUT_BROTHER)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_HERB_WOMAN)
		case DAY_WEDNESDAY:
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_BARGAIN_MAN)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_OLDER_HAIRCUT_BROTHER)
			clearflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_YOUNGER_HAIRCUT_BROTHER)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_HERB_WOMAN)
		case DAY_THURSDAY:
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_BARGAIN_MAN)
			clearflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_OLDER_HAIRCUT_BROTHER)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_YOUNGER_HAIRCUT_BROTHER)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_HERB_WOMAN)
		case DAY_FRIDAY:
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_BARGAIN_MAN)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_OLDER_HAIRCUT_BROTHER)
			clearflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_YOUNGER_HAIRCUT_BROTHER)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_HERB_WOMAN)
		case DAY_SATURDAY:
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_BARGAIN_MAN)
			clearflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_OLDER_HAIRCUT_BROTHER)
			setflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_YOUNGER_HAIRCUT_BROTHER)
			clearflag(FLAG_HIDE_GOLDENROD_CITY_UNDERGROUND_HERB_WOMAN)
		}
	}
}

script GoldenrodCity_Underground_EventScript_BargainMerchant {
	lock
	faceplayer
	dotimebasedevents
	gettime
	if (var(VAR_0x8002) != TIME_MORNING ||
		var(VAR_0x8003) != DAY_MONDAY)
	{
		msgbox("We're not open today.")
	}
	elif (flag(FLAG_DAILY_GOLDENROD_UNDERGROUND_BARGAINS))
	{
		msgbox("Sorry, I'm closing up shop.\n"
			   "Come back another time.")
	}
	else
	{
		msgbox("Hiya!\n"
			   "Care to see some bargains?\p"
			   "I sell rare items that nobody else\n"
			   "carries--but only one of each item.")
		special(BargainShop)
		waitstate
		msgbox("Come by again sometime.")
	}
	release
}

script GoldenrodCity_Underground_EventScript_HerbMerchant {
	lock
	faceplayer
	dotimebasedevents
	gettime
	if (var(VAR_0x8003) == DAY_SUNDAY ||
		var(VAR_0x8003) == DAY_SATURDAY)
	{
		msgbox("Hello, dear.\n"
			   "I sell inexpensive herbal medicine.\p"
			   "They're good but a trifle bitter.\n"
			   "Your {POKEMON} may not like them.\l"
			   "Hehehehe…")
		special(HerbShop)
		waitstate
		msgbox("Come again, dear.\n"
			   "Hehehehe…")
	}
	else
	{
		msgbox("We're not open today.")
	}
	release
}

script GoldenrodCity_Underground_EventScript_OlderHaircutBrother {
	lock
	faceplayer
	dotimebasedevents
	gettime
	if (var(VAR_0x8003) == DAY_TUESDAY ||
		var(VAR_0x8003) == DAY_THURSDAY ||
		var(VAR_0x8003) == DAY_SATURDAY)
	{
		if (flag(FLAG_DAILY_GOLDENROD_UNDERGROUND_HAIRCUT))
		{
			msgbox("I do only one haircut a day.\n"
				   "I'm done for today.")
		}
		else
		{
			showmoneybox(0, 0, FALSE)
			msgbox("Welcome!\n"
				   "I run the {POKEMON} SALON!\p"
				   "I'm the older and better of the two\n"
				   "HAIRCUT BROTHERS.\p"
				   "I can make your {POKEMON} beautiful\n"
				   "for just ¥500.\p"
				   "Would you like me to do that?", MSGBOX_YESNO)
			if (var(VAR_RESULT) == YES)
			{
				checkmoney(500, FALSE)
				if (var(VAR_RESULT) == TRUE)
				{
					msgbox("Which {POKEMON} should I work on?")
					special(ChoosePartyMon)
					waitstate
					if(var(VAR_0x8004) == 0xFF)
					{
						call(GoldenrodCity_Underground_EventScript_OlderHaircutBrother_Rejected)
						release
						end
					}
					special(IsSelectedMonEgg)
					if(var(VAR_RESULT))
					{
						msgbox("I can't do anything with an EGG!\nThat's a shame!", MSGBOX_DEFAULT)
						release
						end
					}
					removemoney(500, FALSE)
					showmoneybox(0, 0, FALSE)
					playse(SE_RG_SHOP)
					msgbox("Okay!\n"
							"Watch it become beautiful!")
					closemessage
					hidemoneybox
					fadescreen(FADE_TO_BLACK)
					special(OlderHaircutBrother)
					playfanfare(MUS_HEAL)
					waitfanfare
					fadescreen(FADE_FROM_BLACK)
					msgbox("There!\n"
							"All done!")
					call(GoldenrodCity_Underground_EventScript_Haircut_Result)
					setflag(FLAG_DAILY_GOLDENROD_UNDERGROUND_HAIRCUT)
				}
				else
				{
					msgbox("You'll need more money than that.")
				}
			}
			else
			{
				call(GoldenrodCity_Underground_EventScript_OlderHaircutBrother_Rejected)
			}
			hidemoneybox
		}
	}
	else
	{
		msgbox("We're not open today.")
	}
	release
}

script GoldenrodCity_Underground_EventScript_OlderHaircutBrother_Rejected {
	msgbox("Is that right?\n"
		   "That's a shame!")
}

script GoldenrodCity_Underground_EventScript_YoungerHaircutBrother {
	lock
	faceplayer
	dotimebasedevents
	gettime
	if (var(VAR_0x8003) == DAY_SUNDAY ||
		var(VAR_0x8003) == DAY_WEDNESDAY ||
		var(VAR_0x8003) == DAY_FRIDAY)
	{
		if (flag(FLAG_DAILY_GOLDENROD_UNDERGROUND_HAIRCUT))
		{
			msgbox("I can do only one haircut a day.\n"
				   "Sorry, but I'm all done for today.")
		}
		else
		{
			showmoneybox(0, 0, FALSE)
			msgbox("Welcome to the {POKEMON} SALON!\p"
				   "I'm the younger and less expensive\n"
				   "of the two HAIRCUT BROTHERS.\p"
				   "I'll spiff up your {POKEMON} for\n"
				   "just ¥300.\p"
				   "So? How about it?", MSGBOX_YESNO)
			if (var(VAR_RESULT) == YES)
			{
				checkmoney(300, FALSE)
				if (var(VAR_RESULT) == TRUE)
				{
					msgbox("Okay, which {POKEMON} should I do?")
					special(ChoosePartyMon)
					waitstate
					if(var(VAR_0x8004) == 0xFF)
					{
						call(GoldenrodCity_Underground_EventScript_YoungerHaircutBrother_Rejected)
						release
						end
					}
					special(IsSelectedMonEgg)
					if(var(VAR_RESULT))
					{
						msgbox("I can't do anything with an EGG!\nHow disappointing!", MSGBOX_DEFAULT)
						release
						end
					}
					removemoney(300, FALSE)
					showmoneybox(0, 0, FALSE)
					playse(SE_RG_SHOP)
					msgbox("Okay!\n"
							"I'll make it look cool!")
					closemessage
					hidemoneybox
					fadescreen(FADE_TO_BLACK)
					special(YoungerHaircutBrother)
					playfanfare(MUS_HEAL)
					waitfanfare
					fadescreen(FADE_FROM_BLACK)
					msgbox("There we go!\n"
							"All done!")
					call(GoldenrodCity_Underground_EventScript_Haircut_Result)
					setflag(FLAG_DAILY_GOLDENROD_UNDERGROUND_HAIRCUT)
				}
				else
				{
					msgbox("You're a little short on funds.")
				}
			}
			else
			{
				call(GoldenrodCity_Underground_EventScript_YoungerHaircutBrother_Rejected)
			}
			hidemoneybox
		}
	}
	else
	{
		msgbox("We're not open today.")
	}
	release
}

script GoldenrodCity_Underground_EventScript_YoungerHaircutBrother_Rejected {
	msgbox("No?\n"
		   "How disappointing!")
}

script GoldenrodCity_Underground_EventScript_Haircut_Result {
	bufferpartymonnick(0, VAR_0x8004)
	special(PlayChosenMonCry)
	textcolor(MSG_COLOR_SYS)
	switch (var(VAR_RESULT))
	{
	case 0:
		msgbox("{STR_VAR_1} looks a little happier.")
	case 1:
		msgbox("{STR_VAR_1} looks happy.")
	case 2:
		msgbox("{STR_VAR_1} looks delighted!")
	}
	waitmoncry
	textcolor(MSG_COLOR_PREV)
}

script GoldenrodCity_Underground_EventScript_Eric {
    trainerbattle_single(TRAINER_ERIC, "I got booted out of the GAME\nCORNER.\pI was trying to cheat using my\n{POKEMON}…", "…Grumble…")
	msgbox("I guess I have to do things fair and\nsquare…", MSGBOX_DEFAULT)
	release
}

script GoldenrodCity_Underground_EventScript_Teru {
    trainerbattle_single(TRAINER_TERU, "Do you consider type alignments in\nbattle?\pIf you know your type advantages,\nyou'll do better in battle.", "Ow, ow, ow!")
	msgbox("I know my {POKEMON} type alignments.\nBut I only use one type of {POKEMON}.", MSGBOX_DEFAULT)
	release
}

script GoldenrodCity_Underground_EventScript_Isaac {
    trainerbattle_single(TRAINER_ISAAC, "My {POKEMON} just got a haircut!\nI'll show you how strong it is!", "Aiyeeee!")
	msgbox("Your {POKEMON} will like you more if\nyou give them haircuts.", MSGBOX_DEFAULT)
	release
}

script GoldenrodCity_Underground_EventScript_Donald {
    trainerbattle_single(TRAINER_DONALD, "I think you have some rare {POKEMON}\nwith you.\pLet me see them!", "Gaah! I lost!\nThat makes me mad!")
	msgbox("Are you making a {POKEDEX}?\nHere's a hot tip.\pThe HIKER on ROUTE 33, ANTHONY, is\na good guy.\pHe'll phone you if he sees any rare\n{POKEMON}.", MSGBOX_DEFAULT)
	release
}

script GoldenrodCity_Underground_EventScript_Door {
	lock
	if (flag(FLAG_GOLDENROD_CITY_UNDERGROUND_DOOR_UNLOCKED))
	{
		msgbox("The door is open.")
	}
	else
	{
		checkitem(ITEM_BASEMENT_KEY, 1)
		if (var(VAR_RESULT))
		{
			message("The BASEMENT KEY opened the door.")
			waitmessage
			playse(SE_UNLOCK)
			waitse
			waitbuttonpress
			closemessage
			setmetatile(24, 8, METATILE_Underground_Door_Open, TRUE)
			special(DrawWholeMapView)
			setflag(FLAG_GOLDENROD_CITY_UNDERGROUND_DOOR_UNLOCKED)
		}
		else
		{
			msgbox("The door's locked…")
		}
	}
	release
}

script GoldenrodCity_Underground_EventScript_NoEntry {
	msgbox("NO ENTRY BEYOND THIS POINT", MSGBOX_SIGN)
}
