mapscripts BlackthornCity_House2_MapScripts {}

script BlackthornCity_House2_EventScript_Emy {
    lock
    faceplayer
    setvar(VAR_0x8008, INGAME_TRADE_DODRIO)
    copyvar(VAR_0x8004, VAR_0x8008)
    specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
    copyvar(VAR_0x8009, VAR_RESULT)

    if (!flag(FLAG_BLACKTHORN_CITY_NPC_TRADE_COMPLETED))
    {
        msgbox("{STR_VAR_1}'s cute, but I don't have\nit. Do you have {STR_VAR_1}♀?\lWant to trade it for my {STR_VAR_2}?", MSGBOX_YESNO)
        
        if (var(VAR_RESULT) != NO)
        {
            special(ChoosePartyMon)
            waitstate
            lock
            faceplayer
            copyvar(VAR_0x800A, VAR_0x8004)
            
            if (var(VAR_0x8004) != 0xFF)
            {
                copyvar(VAR_0x8005, VAR_0x800A)
                specialvar(VAR_RESULT, GetTradeSpecies) // dumps selected party species to VAR_RESULT
                copyvar(VAR_0x800B, VAR_RESULT)

                if (var(VAR_RESULT) == VAR_0x8009) // 0x8009 is requested species
                {
                    msgbox("OK, connect the Game Link Cable.", MSGBOX_DEFAULT)
                    copyvar(VAR_0x8004, VAR_0x8008)
                    copyvar(VAR_0x8005, VAR_0x800A)
                    special(CreateInGameTradePokemon)
                    special(DoInGameTradeScene)
                    waitstate
                    lock
                    faceplayer
                    bufferspeciesname(0, VAR_0x8009)
                    msgbox("Wow! Thank you!\nI always wanted {STR_VAR_1}!")
                    setflag(FLAG_BLACKTHORN_CITY_NPC_TRADE_COMPLETED)
                }
                else
                {
                    bufferspeciesname(0, VAR_0x8009)
                    msgbox("That's not {STR_VAR_1}♀.\nPlease trade with me if you get one.")
                }
            }
            else
            {
                msgbox(BlackthornCity_House2_Text_Emy_NoTrade)
            }
        }
        else
        {
            msgbox(BlackthornCity_House2_Text_Emy_NoTrade)
        }
    }
    else
    {
        bufferspeciesname(0, VAR_0x8009)
        msgbox("How is that {STR_VAR_2} I traded you\ndoing? Your {STR_VAR_1}'s so cute!")
    }
    release
}text BlackthornCity_House2_Text_Emy_NoTrade {
    "You don't want to trade?\nOh, darn…"
}