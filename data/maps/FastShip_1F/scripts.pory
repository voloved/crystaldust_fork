mapscripts FastShip_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION{
        setflag(FLAG_TEMP_3) // Grandpa
        setflag(FLAG_TEMP_4) // Scott
        getplayerxy(VAR_0x8008, VAR_RESULT)
        if(var(VAR_0x8008) == 36 && var(VAR_RESULT) == 1)
        {
            setobjectxyperm(1, 35, 2)
            setobjectmovementtype(1, MOVEMENT_TYPE_FACE_RIGHT)
            if(flag(FLAG_VISITED_VERMILION_CITY))
            {
                setvar(VAR_TEMP_1, 1) // deactivate Grandpa triggers
            }
        }
        else
        {
            setvar(VAR_TEMP_0, 1)
            setvar(VAR_TEMP_1, 1)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_0, 0{
            lock
            applymovement(OBJ_EVENT_ID_PLAYER, FastShip_1F_Movement_PlayerEntersShip)
            waitmovement(0)
            applymovement(1, FastShip_1F_Movement_SailorBlocksEntry)
            waitmovement(0)
            delay(30)
            playse(SE_RG_SS_ANNE_HORN)
            special(DoSealedChamberShakingEffect2)
            waitstate
            special(DoSealedChamberShakingEffect2)
            waitstate
            setrespawn(HEAL_LOCATION_FAST_SHIP_CABIN)
            setobjectxyperm(1, 36, 2)
            setobjectmovementtype(1, MOVEMENT_TYPE_FACE_DOWN)
            setvar(VAR_TEMP_0, 1)
            if(flag(FLAG_VISITED_VERMILION_CITY))
            {
                setvar(VAR_TEMP_1, 1) // deactivate Grandpa triggers
                release
                end
            }
            release
            end
        }
    ]
}movement(local) FastShip_1F_Movement_PlayerEntersShip{
    walk_down
}movement(local) FastShip_1F_Movement_SailorBlocksEntry{
    walk_right
    face_down
}

script FastShip_1F_EventScript_GrandpaTriggers{
    lock
    getplayerxy(VAR_RESULT, VAR_0x8008)
    switch(var(VAR_RESULT))
    {
        case 35:
            setobjectxyperm(2, 27, 8)
        case 36:
            setobjectxyperm(2, 28, 8)
        case 37:
            setobjectxyperm(2, 29, 8)
    }
    addobject(2)
    applymovement(2, FastShip_1F_Movement_GrandpaRunsIn)
    waitmovement(0)
    playse(SE_NOT_EFFECTIVE)
    applymovement(OBJ_EVENT_ID_PLAYER, FastShip_1F_Movement_PlayerShoved)
    applymovement(2, FastShip_1F_Movement_GrandpaFinishesEntrance)
    waitmovement(0)
    textcolor(0)
    msgbox("Whoa! Excuse me. I was in a hurry.\nMy granddaughter is missing!\pShe's just a wee girl.\nIf you see her, please let me know!", MSGBOX_DEFAULT)
    closemessage
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
    switch(var(VAR_RESULT))
    {
        case 35:
            applymovement(2, FastShip_1F_Movement_GrandpaRunsOutLeft)
            waitmovement(0)
        case 36:
            applymovement(2, FastShip_1F_Movement_GrandpaRunsOutMid)
            waitmovement(0)
        case 37:
            applymovement(2, FastShip_1F_Movement_GrandpaRunsOutRight)
            waitmovement(0)
    }
    setflag(FLAG_TEMP_3)
    removeobject(2)
    setvar(VAR_TEMP_1, 1)
    release
    end
}movement(local) FastShip_1F_Movement_GrandpaRunsIn{
    walk_fast_right * 7
}movement(local) FastShip_1F_Movement_PlayerShoved{
    walk_fast_right
    face_left
}movement(local) FastShip_1F_Movement_GrandpaFinishesEntrance{
    walk_right
}movement(local) FastShip_1F_Movement_GrandpaRunsOutLeft{
    walk_fast_down
    walk_fast_right * 9
}movement(local) FastShip_1F_Movement_GrandpaRunsOutMid{
    walk_fast_down
    walk_fast_right * 8
    walk_fast_down * 5
}movement(local) FastShip_1F_Movement_GrandpaRunsOutRight{
    walk_fast_down
    walk_fast_right * 7
    walk_fast_down * 5
}

script FastShip_1F_EventScript_DoorSailor{
    lock
    faceplayer
    if(flag(FLAG_FAST_SHIP_DESTINATION_OLIVINE))
    {
        bufferstring(0, "OLIVINE")
    }
    else
    {
        bufferstring(0, "VERMILION")
    }
    if(flag(FLAG_FAST_SHIP_HAS_ARRIVED))
    {
        msgbox("FAST SHIP S.S. AQUA has arrived\nin {STR_VAR_1} CITY.", MSGBOX_DEFAULT)
        closemessage
        getplayerxy(VAR_RESULT, VAR_0x8008)
        switch(var(VAR_RESULT))
        {
            case 35:
                applymovement(1, FastShip_1F_Movement_DoorSailorMovesRight)
                waitmovement(0)
                if(!flag(FLAG_VISITED_VERMILION_CITY))
                {
                    call(FastShip_1F_EventScript_ScottEncounter)
                }
                applymovement(OBJ_EVENT_ID_PLAYER, FastShip_1F_Movement_PlayerLeavesShipLeft)
                waitmovement(0)
            case 36:
                applymovement(1, FastShip_1F_Movement_DoorSailorMovesRight)
                waitmovement(0)
                if(!flag(FLAG_VISITED_VERMILION_CITY))
                {
                    call(FastShip_1F_EventScript_ScottEncounter)
                }
                applymovement(OBJ_EVENT_ID_PLAYER, FastShip_1F_Movement_PlayerLeavesShipMid)
                waitmovement(0)
            case 37:
                applymovement(1, FastShip_1F_Movement_DoorSailorMovesLeft)
                waitmovement(0)
                if(!flag(FLAG_VISITED_VERMILION_CITY))
                {
                    call(FastShip_1F_EventScript_ScottEncounter)
                }
                applymovement(OBJ_EVENT_ID_PLAYER, FastShip_1F_Movement_PlayerLeavesShipRight)
                waitmovement(0)
        }
        opendoor(36, 1)
        waitdooranim
        applymovement(OBJ_EVENT_ID_PLAYER, FastShip_1F_Movement_PlayerLeavesShip)
        waitmovement(0)
        closedoor(36, 1)
        waitdooranim
        if(flag(FLAG_FAST_SHIP_DESTINATION_OLIVINE))
        {
            clearflag(FLAG_FAST_SHIP_DESTINATION_OLIVINE)
            warp(MAP_OLIVINE_CITY_PORT, 1, 0, 0)
            waitstate
        }
        else
        {
            clearflag(FLAG_FAST_SHIP_DESTINATION_OLIVINE)
            warp(MAP_VERMILION_CITY_PORT, 1, 0, 0)
            waitstate
        }
    }
    else
    {
        msgbox("FAST SHIP S.S. AQUA is en route\nto {STR_VAR_1} CITY.\pWe will make an announcement when\nwe arrive.", MSGBOX_DEFAULT)
        closemessage
        release
        end
    }
}movement(local) FastShip_1F_Movement_DoorSailorMovesRight{
    walk_right
    face_left
}movement(local) FastShip_1F_Movement_DoorSailorMovesLeft{
    walk_left
    face_right
}movement(local) FastShip_1F_Movement_PlayerLeavesShipLeft{
    walk_right
    face_up
}movement(local) FastShip_1F_Movement_PlayerLeavesShipMid{
    walk_up
}movement(local) FastShip_1F_Movement_PlayerLeavesShipRight{
    walk_left
    face_up
}movement(local) FastShip_1F_Movement_PlayerLeavesShip{
    walk_up
    set_invisible
}script(local) FastShip_1F_EventScript_ScottEncounter{
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    playse(SE_PIN)
    delay(0x40)
    switch(var(VAR_RESULT))
    {
        case 35:
            setobjectxyperm(5, 35, 8)
            addobject(5)
        case 36:
            setobjectxyperm(5, 36, 9)
            addobject(5)
        case 37:
            setobjectxyperm(5, 37, 8)
            addobject(5)
    }
    applymovement(5, FastShip_1F_Movement_ScottToPlayer)
    waitmovement(0)
    textcolor(0)
    msgbox("Excuse me!\pI couldn't help but overhear another\npassenger say that the CHAMPION\lhelped find his lost granddaughter.\pThat was you, wasn't it?\pLet me introduce myself.\nMy name's SCOTT.\pI've been traveling everywhere in\nsearch of outstanding trainers.\pMore specifically, I'm looking for\n{POKEMON} battle experts.\pThere's a place I'd like to\ninvite someone like you.\pIt's theâ€¦\nBATTLE FRONTIER!\pI'm going to head back there soon\nmyself.\pThe port is on ROUTE 40 right next\nto OLIVINE CITY.\pI'll tell the ferryman there to\ntake you to check it out yourself!\pWell, I'll be seeing you!", MSGBOX_DEFAULT)
    closemessage
    switch(var(VAR_RESULT))
    {
        case 35:
            applymovement(5, FastShip_1F_Movement_ScottExitsLeft)
            waitmovement(0)
        case 36:
            applymovement(5, FastShip_1F_Movement_ScottExitsMid)
            waitmovement(0)
        case 37:
            applymovement(5, FastShip_1F_Movement_ScottExitsRight)
            waitmovement(0)
    }
    opendoor(36, 1)
    waitdooranim
    applymovement(5, FastShip_1F_Movement_PlayerLeavesShip)
    waitmovement(0)
    closedoor(36, 1)
    waitdooranim
    removeobject(5)
    return
}movement(local) FastShip_1F_Movement_ScottToPlayer{
    walk_up * 5
}movement(local) FastShip_1F_Movement_ScottExitsLeft{
    walk_right
    walk_up
}movement(local) FastShip_1F_Movement_ScottExitsMid{
    walk_left
    walk_up * 2
    walk_right
    face_up
}movement(local) FastShip_1F_Movement_ScottExitsRight{
    walk_left
    walk_up
}



script FastShip_1F_EventScript_CabinSailor{
    if(!flag(FLAG_VISITED_VERMILION_CITY))
    {
        msgbox("Here's your cabin.\pIf your {POKEMON} are hurt, take a\nnap in the bed.\lThat will heal them.", MSGBOX_NPC)
        end
    }
    msgbox("Here's your cabin.\pYou can heal your {POKEMON} by taking\na nap in the bed.\pThe ship will arrive while\nyou're sleeping.", MSGBOX_NPC)
    end
}

script FastShip_1F_EventScript_HallwaySailor{
    msgbox("The passengers are all trainers.\pThey're all itching to battle in\ntheir cabins.", MSGBOX_NPC)
    end
}