mapscripts Route43_Gatehouse2_MapScripts {
    MAP_SCRIPT_ON_TRANSITION{
        if(!flag(FLAG_CLEARED_ROCKET_HIDEOUT))
        {
            setflag(FLAG_TEMP_1)
            getplayerxy(VAR_0x8008, VAR_RESULT)
            if(var(VAR_RESULT) > 3)
            {
                setobjectxyperm(1, 3, 7)
                setobjectxyperm(2, 5, 7)
            }
            end
        }
        setvar(VAR_TEMP_0, 1)
        end
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0 {
            lockall
            textcolor(0)
            playbgm(MUS_ROCKET_TAKEOVER, 0)
            getplayerxy(VAR_0x8008, VAR_RESULT)
            if(var(VAR_RESULT) <= 2)
            {   // at the top
                applymovement(2, Common_Movement_ExclamationMark)
                playse(SE_PIN)
                delay(0x40)
                applymovement(2, Route43_Gatehouse2_Movement_Grunt2BlocksSouth)
                waitmovement(0)
                turnobject(1, DIR_NORTH)
                applymovement(1, Common_Movement_ExclamationMark)
                playse(SE_PIN)
                delay(0x40)
                applymovement(1, Route43_Gatehouse2_Movement_Grunt1BlocksSouth)
                waitmovement(0)
                msgbox(Route43_Gatehouse2_Text_TollRoad)
                checkmoney(1000, 0)
                if(!var(VAR_RESULT))
                {
                    message(Route43_Gatehouse2_Text_NotEnoughMoney)
                }
                else
                {
                    message(Route43_Gatehouse2_Text_ThankYou)
                }
                waitmessage
                playse(SE_SHOP)
                waitse
                waitbuttonpress
                release
                removemoney(1000, 0)
                applymovement(1, Route43_Gatehouse2_Movement_Grunt1LetsPassSouth)
                waitmovement(0)
                applymovement(2, Route43_Gatehouse2_Movement_Grunt2LetsPassSouth)
                waitmovement(0)
                fadedefaultbgm
                setvar(VAR_TEMP_0, 1)
                releaseall
                end
            }
            // at the bottom
            applymovement(1, Common_Movement_ExclamationMark)
            playse(SE_PIN)
            delay(0x40)
            applymovement(1, Route43_Gatehouse2_Movement_Grunt1BlocksNorth)
            waitmovement(0)
            turnobject(2, DIR_NORTH)
            applymovement(2, Common_Movement_ExclamationMark)
            playse(SE_PIN)
            delay(0x40)
            applymovement(2, Route43_Gatehouse2_Movement_Grunt2BlocksNorth)
            waitmovement(0)
            msgbox(Route43_Gatehouse2_Text_TollRoad)
            checkmoney(1000, 0)
            if(!var(VAR_RESULT))
            {
                message(Route43_Gatehouse2_Text_NotEnoughMoney)
            }
            else
            {
                message(Route43_Gatehouse2_Text_ThankYou)
            }
            waitmessage
            playse(SE_SHOP)
            waitse
            waitbuttonpress
            release
            removemoney(1000, 0)
            applymovement(2, Route43_Gatehouse2_Movement_Grunt2LetsPassNorth)
            waitmovement(0)
            applymovement(1, Route43_Gatehouse2_Movement_Grunt1LetsPassNorth)
            waitmovement(0)
            fadedefaultbgm
            setvar(VAR_TEMP_0, 1)
            releaseall
            end
        }
    ]
}movement Route43_Gatehouse2_Movement_Grunt1BlocksSouth{
    walk_fast_up * 4
    face_right
}movement Route43_Gatehouse2_Movement_Grunt2BlocksSouth{
    walk_fast_up * 3
    walk_fast_left
    face_up
}movement Route43_Gatehouse2_Movement_Grunt1LetsPassSouth{
    walk_fast_down * 4
}movement Route43_Gatehouse2_Movement_Grunt2LetsPassSouth{
    walk_fast_down * 3
    walk_fast_right
    face_up
}movement Route43_Gatehouse2_Movement_Grunt1BlocksNorth{
    walk_fast_down * 2
    walk_fast_right
    face_down
}movement Route43_Gatehouse2_Movement_Grunt2BlocksNorth{
    walk_fast_down * 3
    face_left
}movement Route43_Gatehouse2_Movement_Grunt1LetsPassNorth{
    walk_fast_left
    walk_fast_up * 2
    face_down
}movement Route43_Gatehouse2_Movement_Grunt2LetsPassNorth{
    walk_fast_up * 3
    face_up
}text Route43_Gatehouse2_Text_TollRoad{
    "Hold it there, kiddo!\nThe toll is Â¥1000 to go through."
}text Route43_Gatehouse2_Text_NotEnoughMoney{
    "Then pay what you have, please."
}text Route43_Gatehouse2_Text_ThankYou{
    "Thank you very much!"
}

script Route43_Gatehouse2_Eventscript_Rockets {
    msgbox("He-he-he. We're making a bundle.\pEveryone wants to see what's going\non up at LAKE OF RAGE.", MSGBOX_NPC)
    end
}

script Route43_Gatehouse2_Eventscript_Officer {
    if(flag(FLAG_GOT_TM36_SLUDGE_BOMB))
    {
        msgbox("Use this gate to avoid walking in\nthe grass.", MSGBOX_NPC)
        end
    }
    msgbox("I got chased from my post by these\nthugs in black.\pThey left this behind.\pIt makes me uncomfortable.\nCould you take it away?", MSGBOX_DEFAULT)
    checkitemspace(ITEM_TM36, 1)
    if(!var(VAR_RESULT))
    {
        call(Common_EventScript_BagIsFull)
        release
        end
    }
    giveitem(ITEM_TM36, 1)
    setflag(FLAG_GOT_TM36_SLUDGE_BOMB)
    release
    end
}