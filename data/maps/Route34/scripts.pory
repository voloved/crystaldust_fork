mapscripts Route34_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (flag(FLAG_PENDING_DAYCARE_EGG)) {
            clearflag(FLAG_HIDE_ROUTE_34_DAYCARE_MAN_OUTSIDE)
        } else {
            setflag(FLAG_HIDE_ROUTE_34_DAYCARE_MAN_OUTSIDE)
        }
    }
}

script Route34_EventScript_Todd {
    trainerbattle_single(TRAINER_TODD_1, "I'm confident in my ability to\nraise {POKEMON}.\pWant to see?", Route34_Text_Trainer_ToddLost)
    specialvar(VAR_RESULT, ShouldTryRematchBattle)
    if(var(VAR_RESULT) == TRUE)
    {
        trainerbattle_rematch(TRAINER_TODD_1, "I waited for you!\pI even cut back my shopping to\nraise my {POKEMON} better!", Route34_Text_Trainer_ToddLost)
        end
    }
    if (!flag(FLAG_PHONE_CARD_CAMPER_TODD))
    {
        if (!flag(FLAG_CAMPER_TODD_OFFERED_NUMBER))
        {
            bufferstring(0, Route34_Text_Trainer_Todd_AskForNumberEnd)
            msgbox("Maybe I should take one to the\nDAY CARE nearby.\lOr maybe use some items…\pFrom here, I can see GOLDENROD's\nDEPT. STORE.\pThey have bargain sales up on the\nrooftop every so often.\pCould I get your phone number?\nI'll {STR_VAR_1}", MSGBOX_YESNO)
            setflag(FLAG_CAMPER_TODD_OFFERED_NUMBER)
        }
        else
        {
            bufferstring(0, Route34_Text_Trainer_Todd_AskForNumberEnd)
            msgbox("If you give me your number, I'll\n{STR_VAR_1}", MSGBOX_YESNO)
        }
        if (var (VAR_RESULT) == YES)
        {
            registerphonecontact(FLAG_PHONE_CARD_CAMPER_TODD)
            msgbox(Route34_Text_Trainer_ToddAfterRegistered, MSGBOX_AUTOCLOSE)
        }
        else
        {
            msgbox("Anyway… If you're short on money,\ntake down my number.\lI'll call you when there's a sale.", MSGBOX_AUTOCLOSE)
        }
        end
    }
    if(flag(FLAG_DAILY_GOLDENROD_DEPT_STORE_SALE_HAPPENING))
    {
        msgbox("Shopping under the sky!\nIt feels so nice up on a rooftop.", MSGBOX_AUTOCLOSE)
        end
    }
    msgbox(Route34_Text_Trainer_ToddAfterRegistered, MSGBOX_AUTOCLOSE)
}text(local) Route34_Text_Trainer_ToddLost {
    "Did I screw up my training?"
}text(local) Route34_Text_Trainer_Todd_AskForNumberEnd {
    "call you when they have a sale."
}text(local) Route34_Text_Trainer_ToddAfterRegistered{
    "I check GOLDENROD DEPT. STORE\nevery day, so I know when there's\la sale."
}

script Route34_EventScript_Samuel {
    trainerbattle_single(TRAINER_SAMUEL, "This is where I do my training!", "Beaten by a passing stranger!")
    msgbox("Have you been to GOLDENROD CITY?\pWeren't you amazed by how they've\nchanged the {POKEMON} CENTER?", MSGBOX_AUTOCLOSE)
}

script Route34_EventScript_Ian {
    trainerbattle_single(TRAINER_IAN, "I'm the best in my class at {POKEMON}.", "No!\nThere are better TRAINERS…")
    msgbox("I'm trying hard so I can be the\nstar in my class.", MSGBOX_AUTOCLOSE)
}

script Route34_EventScript_Gina {
    trainerbattle_single(TRAINER_GINA_1, "Are you a TRAINER?\nLet's have a practice battle.", Route34_Text_Trainer_GinaLost)
    // TODO: Check phone strings for textbox fit
    if(flag(FLAG_CALL_GINA_GIFT))
    {
        msgbox("So you're finally here!\nI think this will make you happy.", MSGBOX_AUTOCLOSE)
        giveitem(ITEM_LEAF_STONE, 1)
        if(var(VAR_0x8007) == FALSE)
        {
            msgbox("Your PACK looks completely full.\nI'll have to hold on to this.", MSGBOX_AUTOCLOSE)
        }
        else
        {
            clearflag(FLAG_CALL_GINA_GIFT)
        }
        release
        end
    }
    specialvar(VAR_RESULT, ShouldTryRematchBattle)
    if(var(VAR_RESULT) == TRUE)
    {
        trainerbattle_rematch(TRAINER_GINA_1, "I've been waiting!\nI've gotten a bit better at this!", Route34_Text_Trainer_GinaLost)
        end
    }
    if (!flag(FLAG_PHONE_CARD_PICNICKER_GINA))
    {
        if (!flag(FLAG_PICNICKER_GINA_OFFERED_NUMBER))
        {
            bufferstring(0, Route34_Text_Trainer_Gina_AskForNumberEnd)
            msgbox("You're too strong to be a practice\npartner.\pWhenever I see a strong trainer,\nI want to be their cheerleader.\pWhen I'm on my walk,\nI sometimes pick up items.\lIf I get anything, you can have it!\p{STR_VAR_1}\nI'll call as soon as I get anything.", MSGBOX_YESNO)
            setflag(FLAG_PICNICKER_GINA_OFFERED_NUMBER)
        }
        else
        {
            bufferstring(0, Route34_Text_Trainer_Gina_AskForNumberEnd)
            msgbox("If I find an item, you can have it!\n{STR_VAR_1}", MSGBOX_YESNO)
        }
        if (var (VAR_RESULT) == YES)
        {
            registerphonecontact(FLAG_PHONE_CARD_PICNICKER_GINA)
            msgbox(Route34_Text_Trainer_GinaAfterRegistered, MSGBOX_AUTOCLOSE)
        }
        else
        {
            msgbox("Is that so? Well, I'll still be\nrooting for you!", MSGBOX_AUTOCLOSE)
        }
        end
    }
    msgbox(Route34_Text_Trainer_GinaAfterRegistered, MSGBOX_AUTOCLOSE)
}text(local) Route34_Text_Trainer_GinaLost {
    "Oh, no!\nI just can't win…"
}text(local) Route34_Text_Trainer_Gina_AskForNumberEnd {
    "Want to give me your number?"
}text(local) Route34_Text_Trainer_GinaAfterRegistered{
    "I'll call as soon as I get anything!"
}

script Route34_EventScript_Keith {
    lock
    faceplayer
    gettime
    if (var (VAR_0x8002) == TIME_NIGHT) {
        trainerbattle_single(TRAINER_KEITH, "Who goes there?\nWhat are you up to?", "You're a tough little kid.")
        msgbox("Yep, I see nothing wrong today.\nYou be good and stay out of trouble.")
    } else {
        msgbox("I'm on patrol for suspicious\nindividuals.")
    }
    release
}

script Route34_EventScript_Brandon {
    trainerbattle_single(TRAINER_BRANDON, "I just got my {POKEMON} back from the\nDAY CARE.\pLet's see how much stronger it got!", "Why does it end this way?")
    msgbox("My {POKEMON} knew moves I didn't\nknow it had.\pThat confounded me to no end!", MSGBOX_AUTOCLOSE)
}

script Route34_EventScript_Irene {
    trainerbattle_single(TRAINER_IRENE, "IRENE: Kyaaah!\nSomeone found us!", "IRENE: Ohhh!\nToo strong!")
    if (!defeated(TRAINER_KATE)) {
        msgbox("IRENE: My sister KATE will get\nyou for this!")
    } else {
        msgbox("IRENE: Isn't this beach great?\nIt's our secret little getaway!")
    }
    release
}

script Route34_EventScript_Jenn {
    trainerbattle_single(TRAINER_JENN, "JENN: You can't stomp in here like\nthis and go unpunished!", "JENN: So sorry, IRENE!\nSis!")
    if (!defeated(TRAINER_KATE)) {
        msgbox("JENN: Don't get cocky!\nMy sister KATE is tough!")
    } else {
        msgbox("JENN: Sunlight makes your body\nstronger.")
    }
    release
}

script Route34_EventScript_Kate {
    trainerbattle_single(TRAINER_KATE, "KATE: You sure were mean to my\nlittle sisters!", "KATE: No!\nI can't believe I lost.")
    if (!flag(FLAG_RECEIVED_SOFT_SAND_FROM_KATE)) {
        msgbox("KATE: You're too strong.\nI didn't stand a chance.\pHere.\nYou deserve this.")
        giveitem(ITEM_SOFT_SAND)
        if (!var (VAR_RESULT)) {
            goto(Common_EventScript_ShowBagIsFull)
        }
        setflag(FLAG_RECEIVED_SOFT_SAND_FROM_KATE)
    }
    msgbox("KATE: I'm sorry we jumped you.\pWe never expected anyone to find\nus here. You sure startled us.", MSGBOX_AUTOCLOSE)
}

script Route34_EventScript_DirectionsSign {
  msgbox("ROUTE 34\nGOLDENROD CITY - AZALEA TOWN\pILEX FOREST SOMEWHERE BETWEEN", MSGBOX_SIGN)
}

script Route34_EventScript_TrainerTipsSign {
    msgbox("TRAINER TIPS\pBERRY trees grow new BERRIES\nevery day.\pMake a note of which trees bear\nwhich BERRIES.", MSGBOX_SIGN)
}

script Route34_EventScript_DaycareSign {
    msgbox("DAY CARE\nLET US RAISE YOUR {POKEMON} FOR YOU!", MSGBOX_SIGN)
}
