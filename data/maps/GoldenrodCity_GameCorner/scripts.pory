const COINS_PRICE_50  = 1000
const COINS_PRICE_500 = 10000
const TM_PRIZE_COINS  = 5500

mapscripts GoldenrodCity_GameCorner_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        gettime
        if (var(VAR_0x8002) == TIME_MORNING)
        {
            setflag(FLAG_HIDE_GOLDENROD_CITY_GAME_CORNER_NERD)
        }
        else
        {
            clearflag(FLAG_HIDE_GOLDENROD_CITY_GAME_CORNER_NERD)
        }
        if(flag(FLAG_SYS_GAME_CLEAR) && !flag(FLAG_DAILY_GOLDENROD_GAME_CORNER_TUTOR))
        {
            if(var(VAR_0x8003) == DAY_WEDNESDAY || var(VAR_0x8003) == DAY_SATURDAY)
            {
                setflag(FLAG_TEMP_1)
            }
        }
	}
}

script GoldenrodCity_GameCorner_EventScript_CoinClerk {
    bufferstring(1, "GOLDENROD CITY\n")
    goto(GoldenrodCity_GameCorner_EventScript_CoinClerk2)
}

script GoldenrodCity_GameCorner_EventScript_CoinClerk2{
    lock
    faceplayer
    showmoneybox(0, 0, FALSE)
    showcoinsbox(0, 5)
    msgbox("Welcome to the {STR_VAR_2}"
           "GAME CORNER.")
    checkitem(ITEM_COIN_CASE, 1)
    if (!var(VAR_RESULT))
    {
        msgbox("Do you need game coins?\p"
               "Oh, you don't have a COIN CASE\n"
               "for your coins.")
    }
    else
    {
        message("Do you need some game coins?\p"
                "It costs Â¥1000 for 50 coins.\n"
                "Do you want some?")
        waitmessage
        multichoicedefault(13, 0, MULTI_GAME_CORNER_COINS, 0, 0)
        call(GoldenrodCity_GameCorner_EventScript_CoinClerk_ShowChoice)
    }
    hidemoneybox
    hidecoinsbox(0, 5)
    release
}

script GoldenrodCity_GameCorner_EventScript_CoinClerk_ShowChoice {
    switch (var(VAR_RESULT))
    {
        case 0:
            checkcoins(VAR_TEMP_1)
            if (var(VAR_TEMP_1) >= MAX_COINS + 1 - 50)
            {
                call(GoldenrodCity_GameCorner_EventScript_CoinClerk_NoRoom)
            }
            else
            {
                checkmoney(COINS_PRICE_50, FALSE)
                if (var(VAR_RESULT) == FALSE)
                {
                    call(GoldenrodCity_GameCorner_EventScript_CoinClerk_NotEnoughMoney)
                }
                else
                {
                    addcoins(50)
                    removemoney(COINS_PRICE_50, FALSE)
                    updatemoneybox(0, 0)
                    updatecoinsbox(0, 5)
                    playse(SE_RG_SHOP)
                    message("Thank you!\n"
                            "Here are 50 coins.")
                    waitmessage
                    multichoicedefault(13, 0, MULTI_GAME_CORNER_COINS, 0, 0)
                    goto(GoldenrodCity_GameCorner_EventScript_CoinClerk_ShowChoice)
                }
            }
        case 1:
            checkcoins(VAR_TEMP_1)
            if (var(VAR_TEMP_1) >= MAX_COINS + 1 - 500)
            {
                call(GoldenrodCity_GameCorner_EventScript_CoinClerk_NoRoom)
            }
            else
            {
                checkmoney(COINS_PRICE_500, FALSE)
                if (var(VAR_RESULT) == FALSE)
                {
                    call(GoldenrodCity_GameCorner_EventScript_CoinClerk_NotEnoughMoney)
                }
                else
                {
                    addcoins(500)
                    removemoney(COINS_PRICE_500, FALSE)
                    updatemoneybox(0, 0)
                    updatecoinsbox(0, 5)
                    playse(SE_RG_SHOP)
                    message("Thank you!\n"
                            "Here are 500 coins.")
                    waitmessage
                    multichoicedefault(13, 0, MULTI_GAME_CORNER_COINS, 1, 0)
                    goto(GoldenrodCity_GameCorner_EventScript_CoinClerk_ShowChoice)
                }
            }
        default:
            msgbox("No coins for you?\n"
                   "Come again!")
    }
}

script GoldenrodCity_GameCorner_EventScript_CoinClerk_NoRoom {
    msgbox("Whoops!\n"
           "Your COIN CASE is full.")
}

script GoldenrodCity_GameCorner_EventScript_CoinClerk_NotEnoughMoney {
    msgbox("You don't have enough money.")
}

script GoldenrodCity_GameCorner_EventScript_TMClerk {
    lock
    faceplayer
    msgbox(GoldenrodCity_GameCorner_Text_PrizeVendorIntro)
    checkitem(ITEM_COIN_CASE, 1)
    if (!var(VAR_RESULT))
    {
        msgbox(GoldenrodCity_GameCorner_Text_PrizeVendorNoCoinCase)
    }
    else
    {
        message(GoldenrodCity_GameCorner_Text_PrizeClerk_WhichPrize)
        waitmessage
        showcoinsbox(0, 0)
        call(GoldenrodCity_GameCorner_EventScript_TMClerk_ShowChoice)
        hidecoinsbox(0, 0)
    }
    release
}

script GoldenrodCity_GameCorner_EventScript_TMClerk_ReturnToShowChoice {
    message(GoldenrodCity_GameCorner_Text_PrizeClerk_WhichPrize)
    waitmessage
    goto(GoldenrodCity_GameCorner_EventScript_TMClerk_ShowChoice)
}

text GoldenrodCity_GameCorner_Text_PrizeClerk_WhichPrize {
    "Which prize would you like?"
}

script GoldenrodCity_GameCorner_EventScript_TMClerk_ShowChoice {
    multichoice(11, 0, MULTI_GAME_CORNER_TMS, 0)
    switch (var(VAR_RESULT))
    {
        case 0:
            setvar(VAR_TEMP_1, ITEM_TM25_THUNDER)
            call(GoldenrodCity_GameCorner_EventScript_TMClerk_ConfirmPrize)
        case 1:
            setvar(VAR_TEMP_1, ITEM_TM14_BLIZZARD)
            call(GoldenrodCity_GameCorner_EventScript_TMClerk_ConfirmPrize)
        case 2:
            setvar(VAR_TEMP_1, ITEM_TM38_FIRE_BLAST)
            call(GoldenrodCity_GameCorner_EventScript_TMClerk_ConfirmPrize)
        default:
            msgbox(GoldenrodCity_GameCorner_Text_PrizeVendorOutro)
    }
}

script GoldenrodCity_GameCorner_EventScript_TMClerk_ConfirmPrize {
    bufferitemname(0, VAR_TEMP_1)
    copyvar(VAR_0x8004, VAR_TEMP_1)
    special(BufferTMHMMoveName)
    msgbox("So you want the {STR_VAR_1} {STR_VAR_2}.\n"
           "Is that right?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES)
    {
        checkcoins(VAR_RESULT)
        if (var(VAR_RESULT) < TM_PRIZE_COINS)
        {
            msgbox(GoldenrodCity_GameCorner_Text_PrizeVendorNotEnoughCoins)
        }
        else
        {
            checkitemspace(VAR_TEMP_1, 1)
            if (var(VAR_RESULT) == FALSE)
            {
                call(Common_EventScript_BagIsFull)
                goto(GoldenrodCity_GameCorner_EventScript_TMClerk_ReturnToShowChoice)
            }
            else
            {
                removecoins(TM_PRIZE_COINS)
                additem(VAR_TEMP_1)
                updatecoinsbox(0, 0)
                playse(SE_SHOP)
                msgbox(GoldenrodCity_GameCorner_EventScript_PrizeVendorHereYouGo)
            }
        }
    }
    else
    {
        goto(GoldenrodCity_GameCorner_EventScript_TMClerk_ReturnToShowChoice)
    }
}

text GoldenrodCity_GameCorner_Text_PrizeVendorIntro {
    "Welcome!\p"
    "We exchange your game coins for\n"
    "fabulous prizes!"
}

text GoldenrodCity_GameCorner_Text_PrizeVendorNoCoinCase {
    "Oh?\n"
    "You don't have a COIN CASE."
}

text GoldenrodCity_GameCorner_Text_PrizeVendorNotEnoughCoins {
    "Sorry!\n"
    "You need more coins than that."
}

text GoldenrodCity_GameCorner_EventScript_PrizeVendorHereYouGo {
    "Here you go!"
}

text GoldenrodCity_GameCorner_Text_PrizeVendorOutro {
    "Okay. Please save your coins\n"
    "and come again!"
}

script GoldenrodCity_GameCorner_EventScript_MonClerk {
    lock
    faceplayer
    msgbox(GoldenrodCity_GameCorner_Text_PrizeVendorIntro)
    checkitem(ITEM_COIN_CASE, 1)
    if (!var(VAR_RESULT))
    {
        msgbox(GoldenrodCity_GameCorner_Text_PrizeVendorNoCoinCase)
    }
    else
    {
        message(GoldenrodCity_GameCorner_Text_PrizeClerk_WhichPrize)
        waitmessage
        showcoinsbox(0, 0)
        call(GoldenrodCity_GameCorner_EventScript_MonClerk_ShowChoice)
        hidecoinsbox(0, 0)
    }
    release
}

script GoldenrodCity_GameCorner_EventScript_MonClerk_ReturnToShowChoice {
    message(GoldenrodCity_GameCorner_Text_PrizeClerk_WhichPrize)
    waitmessage
    goto(GoldenrodCity_GameCorner_EventScript_MonClerk_ShowChoice)
}

script GoldenrodCity_GameCorner_EventScript_MonClerk_ShowChoice {
    multichoice(11, 0, MULTI_GAME_CORNER_MONS, 0)
    switch (var(VAR_RESULT))
    {
        case 0:
            setvar(VAR_TEMP_1, SPECIES_ABRA)
            setvar(VAR_TEMP_2, 100)
            call(GoldenrodCity_GameCorner_EventScript_MonClerk_ConfirmPrize)
        case 1:
            setvar(VAR_TEMP_1, SPECIES_CUBONE)
            setvar(VAR_TEMP_2, 800)
            call(GoldenrodCity_GameCorner_EventScript_MonClerk_ConfirmPrize)
        case 2:
            setvar(VAR_TEMP_1, SPECIES_WOBBUFFET)
            setvar(VAR_TEMP_2, 1500)
            call(GoldenrodCity_GameCorner_EventScript_MonClerk_ConfirmPrize)
        default:
            msgbox(GoldenrodCity_GameCorner_Text_PrizeVendorOutro)
    }
}

script GoldenrodCity_GameCorner_EventScript_MonClerk_ConfirmPrize {
    bufferspeciesname(0, VAR_TEMP_1)
    msgbox("So you want the {STR_VAR_1}.\n"
           "Is that right?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES)
    {
        checkcoins(VAR_RESULT)
        if (var(VAR_RESULT) < VAR_TEMP_2)
        {
            msgbox(GoldenrodCity_GameCorner_Text_PrizeVendorNotEnoughCoins)
        }
        else
        {
            switch (var(VAR_TEMP_1))
            {
                case SPECIES_ABRA:
                    givemon(VAR_TEMP_1, 5, ITEM_NONE)
                default:
                    givemon(VAR_TEMP_1, 15, ITEM_NONE)
            }

            copyvar(VAR_TEMP_3, VAR_RESULT)
            switch (var(VAR_TEMP_3))
            {
                case MON_CANT_GIVE: // no room anywhere
                    call(Common_EventScript_NoMoreRoomForPokemon)
                    goto(GoldenrodCity_GameCorner_EventScript_MonClerk_ReturnToShowChoice)
                default:
                    removecoins(VAR_TEMP_2)
                    updatecoinsbox(0, 0)
                    playse(SE_SHOP)
                    msgbox(GoldenrodCity_GameCorner_EventScript_PrizeVendorHereYouGo)
                    textcolor(MSG_COLOR_SYS)
                    bufferspeciesname(0, VAR_TEMP_1)
                    playfanfare(MUS_LEVEL_UP)
                    message(gText_PlayerObtainedTheMon)
                    waitmessage
                    waitfanfare
                    msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
                    switch (var(VAR_TEMP_3))
                    {
                        case MON_GIVEN_TO_PARTY: // party
                            if (var(VAR_RESULT) == YES)
                            {
                                call(Common_EventScript_GetGiftMonPartySlot)
                                call(Common_EventScript_NameReceivedPartyMon)
                                showcoinsbox(0, 0)
                            }
                        case MON_GIVEN_TO_PC: // PC
                            if (var(VAR_RESULT) == YES)
                            {
                                call(Common_EventScript_NameReceivedBoxMon)
                                showcoinsbox(0, 0)
                            }
                            call(Common_EventScript_TransferredToPC)
                    }
            }
        }
    }
    textcolor(MSG_COLOR_PREV)
    goto(GoldenrodCity_GameCorner_EventScript_MonClerk_ReturnToShowChoice)
}

script GoldenrodCity_GameCorner_EventScript_Nerd {
    lock
    faceplayer
    msgbox("I always play this slot machine. It\n"
           "pays out more than others, I think.")
    call(GoldenrodCity_GameCorner_EventScript_FaceSlotMachine)
    release
}

script GoldenrodCity_GameCorner_EventScript_PokefanM {
    lock
    faceplayer
    msgbox("I just love this new slot machine.\p"
           "It's more of a challenge than the\n"
           "ones in CELADON.")
    call(GoldenrodCity_GameCorner_EventScript_FaceSlotMachine)
    release
}

script GoldenrodCity_GameCorner_EventScript_GamblinMan {
    lock
    faceplayer
    msgbox("Life is a gamble.\n"
           "I'm going to flip cards till I drop!")
    call(GoldenrodCity_GameCorner_EventScript_FaceSlotMachine)
    release
}

script GoldenrodCity_GameCorner_EventScript_CardFlipWoman {
    lock
    faceplayer
    msgbox("CARD FLIPâ¦\p"
           "I prefer it over the slots because\n"
           "it's easier to figure out the odds.\p"
           "But the payout is much lower.")
    call(GoldenrodCity_GameCorner_EventScript_FaceSlotMachine)
    release
}

script GoldenrodCity_GameCorner_EventScript_WontStopWoman {
    msgbox("I won't quit until I win!", MSGBOX_NPC)
}

script GoldenrodCity_GameCorner_EventScript_Gentleman {
    lock
    faceplayer
    msgbox("I taught BLIZZARD to my {POKEMON}.\p"
           "It was hard to get enough coins for\n"
           "it, but it was worth it.")
    call(GoldenrodCity_GameCorner_EventScript_FaceSlotMachine)
    release
}

script GoldenrodCity_GameCorner_EventScript_CoinCaseMan {
    msgbox("I couldn't win at the slots, and I\n"
           "blew it on card flippingâ¦\p"
           "I got so furious, I tossed out my\n"
           "COIN CASE in the UNDERGROUND.", MSGBOX_NPC)
}

script GoldenrodCity_GameCorner_EventScript_MoveTutorInside {
    lock
    faceplayer
    msgbox("Wahahah!\n"
           "The coins keep rolling in!")
    call(GoldenrodCity_GameCorner_EventScript_FaceSlotMachine)
    release
}

script GoldenrodCity_GameCorner_EventScript_Drink {
    msgbox("Someone left their drink.\n"
           "It smells sweet.", MSGBOX_SIGN)
}

script GoldenrodCity_GameCorner_EventScript_FaceSlotMachine {
    closemessage
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine0 {
    lockall
	setvar(VAR_0x8004, 0)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine1 {
    lockall
	setvar(VAR_0x8004, 1)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine2 {
    lockall
	setvar(VAR_0x8004, 2)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine3 {
    lockall
	setvar(VAR_0x8004, 3)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine4 {
    lockall
	setvar(VAR_0x8004, 4)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine5 {
    lockall
	setvar(VAR_0x8004, 5)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine6 {
    lockall
	setvar(VAR_0x8004, 6)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine7 {
    lockall
	setvar(VAR_0x8004, 7)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine8 {
    lockall
	setvar(VAR_0x8004, 8)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine9 {
    lockall
	setvar(VAR_0x8004, 9)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine10 {
    lockall
	setvar(VAR_0x8004, 10)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine11 {
    lockall
	setvar(VAR_0x8004, 11)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine12 {
    lockall
	setvar(VAR_0x8004, 12)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine13 {
    lockall
	setvar(VAR_0x8004, 13)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine14 {
    lockall
	setvar(VAR_0x8004, 14)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine15 {
    lockall
	setvar(VAR_0x8004, 15)
	goto(GoldenrodCity_GameCorner_EventScript_SlotMachine)
}

script GoldenrodCity_GameCorner_EventScript_CardFlip0 {
    lockall
	setvar(VAR_0x8004, 0)
	goto(GoldenrodCity_GameCorner_EventScript_CardFlip)
}

script GoldenrodCity_GameCorner_EventScript_CardFlip1 {
    lockall
	setvar(VAR_0x8004, 1)
	goto(GoldenrodCity_GameCorner_EventScript_CardFlip)
}

script GoldenrodCity_GameCorner_EventScript_CardFlip2 {
    lockall
	setvar(VAR_0x8004, 2)
	goto(GoldenrodCity_GameCorner_EventScript_CardFlip)
}

script GoldenrodCity_GameCorner_EventScript_CardFlip3 {
    lockall
	setvar(VAR_0x8004, 3)
	goto(GoldenrodCity_GameCorner_EventScript_CardFlip)
}

script GoldenrodCity_GameCorner_EventScript_CardFlip4 {
    lockall
	setvar(VAR_0x8004, 4)
	goto(GoldenrodCity_GameCorner_EventScript_CardFlip)
}

script GoldenrodCity_GameCorner_EventScript_CardFlip5 {
    lockall
	setvar(VAR_0x8004, 5)
	goto(GoldenrodCity_GameCorner_EventScript_CardFlip)
}

script GoldenrodCity_GameCorner_EventScript_CardFlip6 {
    lockall
	setvar(VAR_0x8004, 6)
	goto(GoldenrodCity_GameCorner_EventScript_CardFlip)
}

script GoldenrodCity_GameCorner_EventScript_CardFlip7 {
    lockall
	setvar(VAR_0x8004, 7)
	goto(GoldenrodCity_GameCorner_EventScript_CardFlip)
}

script GoldenrodCity_GameCorner_EventScript_CardFlip8 {
    lockall
	setvar(VAR_0x8004, 8)
	goto(GoldenrodCity_GameCorner_EventScript_CardFlip)
}

script GoldenrodCity_GameCorner_EventScript_CardFlip9 {
    lockall
	setvar(VAR_0x8004, 9)
	goto(GoldenrodCity_GameCorner_EventScript_CardFlip)
}

script GoldenrodCity_GameCorner_EventScript_SlotMachine {
    checkitem(ITEM_COIN_CASE, 1)
    if (!var(VAR_RESULT))
    {
        msgbox("You can't play without a COIN CASE.")
    }
    else
    {
        specialvar(VAR_RESULT, GetSlotMachineId)
        playslotmachine(VAR_RESULT)
    }
    releaseall
}

script GoldenrodCity_GameCorner_EventScript_CardFlip {
    checkitem(ITEM_COIN_CASE, 1)
    if (!var(VAR_RESULT))
    {
        msgbox("You can't play without a COIN CASE.")
    }
    else
    {
        special(PlayCardFlip)
        waitstate
    }
    releaseall
}
