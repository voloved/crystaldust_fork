mapscripts Route35_GoldenrodGatehouse_MapScripts { }

script Route35_GoldenrodGatehouse_EventScript_Officer {
    lock
    faceplayer
    if (flag(FLAG_GOT_HP_UP_FROM_RANDY))
    {
        msgbox(Route35_GoldenrodGatehouse_Text_Officer_PalWasSnoozing)
    }
    elif (flag(FLAG_GAVE_KENYA_TO_RANDYS_FRIEND))
    {
        msgbox("Thanks, kid! You made the delivery\n"
               "for me!\p"
               "Here's something for your trouble!")
        giveitem(ITEM_HP_UP, 1)
        if (!var(VAR_RESULT))
        {
            goto(Common_EventScript_ShowBagIsFull)
        }
        msgbox(Route35_GoldenrodGatehouse_Text_Officer_PalWasSnoozing)
        setflag(FLAG_GOT_HP_UP_FROM_RANDY)
    }
    elif (flag(FLAG_GOT_KENYA_FROM_RANDY))
    {
        msgbox(Route35_GoldenrodGatehouse_Text_Officer_ReadItButDontLoseIt)
    }
    else
    {
        msgbox("Excuse me, kid!\n"
               "Can you do a guy a favor?\p"
               "Can you take this {POKEMON} with MAIL\n"
               "to my friend? He's on ROUTE 31.", MSGBOX_YESNO)
        if (var(VAR_RESULT) == TRUE)
        {
            msgbox("You will? Perfect!\n"
                   "Thanks, kid!\p"
                   "My pal's a chubby guy who snoozes\n"
                   "all the time.\p"
                   "You'll recognize him right away!")
            setvar(VAR_0x8004, INGAME_TRADE_GIFT_SPEAROW)
            special(GivePlayerSpecialGiftMon)
            if (!var(VAR_RESULT))
            {
                msgbox("You can't carry another {POKEMON}…")
            }
            else
            {
                textcolor(MSG_COLOR_SYS)
                playfanfare(MUS_OBTAIN_ITEM)
                message("{PLAYER} received a {POKEMON}\nwith MAIL.")
                waitmessage
                waitfanfare
	            textcolor(MSG_COLOR_PREV)
                msgbox(Route35_GoldenrodGatehouse_Text_Officer_ReadItButDontLoseIt)
                setflag(FLAG_GOT_KENYA_FROM_RANDY)
            }
        }
        else
        {
            msgbox("Oh…\n"
                   "Never mind, then…")
        }
    }
    release
}

text Route35_GoldenrodGatehouse_Text_Officer_ReadItButDontLoseIt {
    "You can read it, but don't lose it!\n"
    "ROUTE 31!\p"
    "Oh, yeah. There was a weird tree\n"
    "blocking the road.\l"
    "I wonder if it's been cleared?"
}

text Route35_GoldenrodGatehouse_Text_Officer_PalWasSnoozing {
    "My pal was snoozing, right?\n"
    "Heh, what'd I say?"
}

script Route35_GoldenrodGatehouse_EventScript_ConcernedWoman {
	lock
	faceplayer
    if (!flag(FLAG_DEFEATED_SUDOWOODO))
    {
        msgbox("A strange tree is blocking the road.\n"
               "It wriggles around if you go near.\p"
               "I heard it became wild when someone\n"
               "watered it with a SQUIRTBOTTLE.")
    }
    else
    {
        msgbox("I like the {POKEMON} Lullaby they play\n"
               "on the radio.")
    }
	release
}

script Route35_GoldenrodGatehouse_EventScript_CuriousMan {
    msgbox("I wonder how many kinds of {POKEMON}\n"
           "there are in the world.\p"
           "Three years ago, PROF. OAK said that\n"
           "there were 150 different kinds.", MSGBOX_NPC)
}
