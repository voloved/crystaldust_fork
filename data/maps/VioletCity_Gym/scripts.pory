mapscripts VioletCity_Gym_MapScripts {
    
}

script VioletCity_Gym_EventScript_Falkner {
    trainerbattle_single(TRAINER_FALKNER, VioletCity_Gym_Text_FalknerPreBattle, VioletCity_Gym_Text_FalknerDefeat, VioletCity_Gym_EventScript_FalknerDefeated, NO_MUSIC)
    if (!flag(FLAG_GOT_TM40_FROM_FALKNER))
    {
        goto(VioletCity_Gym_EventScript_GiveAerialAce)
    }
    msgbox("There are {POKEMON} GYMS in cities\n"
		   "and towns ahead.\p"
		   "You should test your skills at\n"
		   "these GYMS.\p"
		   "I'm going to train harder to become\n"
		   "the greatest bird master!")
	release
}

text VioletCity_Gym_Text_FalknerPreBattle {
	"I'm FALKNER, the VIOLET CITY\n"
	"{POKEMON} GYM LEADER!\p"
	"People say you can clip FLYING-\n"
	"type {POKEMON}'s wings with a jolt\l"
	"of electricity…\p"
	"I won't allow such insults to\n"
	"BIRD {POKEMON}!\p"
	"I'll show you the real power of\n"
	"the magnificent BIRD {POKEMON}!"
}

text VioletCity_Gym_Text_FalknerDefeat {
	"…Darn! My dad's cherished bird\n"
	"{POKEMON}…\p"
	"All right.\n"
	"Take this.\p"
	"It's the official {POKEMON} LEAGUE\n"
	"ZEPHYRBADGE."
}

script VioletCity_Gym_EventScript_FalknerDefeated {
	textcolor(MSG_COLOR_SYS)
    message("{PLAYER} received the ZEPHYRBADGE\n"
            "from FALKNER.")
    waitmessage
    call(Common_EventScript_PlayGymBadgeFanfare)
	textcolor(MSG_COLOR_PREV)
    setflag(FLAG_DEFEATED_VIOLET_GYM)
    setflag(FLAG_BADGE01_GET)
    setflag(FLAG_FORCED_CALL_ELM_AIDE_IN_VIOLET)
    setgymtrainers(1)
    goto(VioletCity_Gym_EventScript_GiveAerialAce)
}

script VioletCity_Gym_EventScript_GiveAerialAce {
    msgbox("The ZEPHYRBADGE raises the ATTACK\n"
           "power of {POKEMON}.\p"
           "It also enables {POKEMON} to use\n"
           "FLASH, if they have it, anytime.\p"
           "Here--take this too.")
    checkitemspace(ITEM_TM40, 1)
    if (!var(VAR_RESULT))
	{
        goto(Common_EventScript_ShowBagIsFull)
	}
	else
	{
		givetmhm_msg("{PLAYER} received TM40\n"
					 "from FALKNER.", ITEM_TM40)
		setflag(FLAG_GOT_TM40_FROM_FALKNER)
		msgbox("By using a TM, a {POKEMON} will\n"
			   "instantly learn a new move.\p"
			   "Think before you act--a TM can\n"
			   "be used only once.\p"
			   "TM40 contains AERIAL ACE.\p"
			   "It lets you attack with unparalleled\n"
			   "speed and precision.\p"
			   "In other words, it never misses.")
	}
	release
}

script VioletCity_Gym_EventScript_Abe {
    trainerbattle_single(TRAINER_ABE, "Let me see if you are good enough\nto face FALKNER!", "This can't be true!")
	msgbox("This is pathetic, losing to some\nrookie TRAINER…", MSGBOX_DEFAULT)
	release
}

script VioletCity_Gym_EventScript_Rod {
    trainerbattle_single(TRAINER_ROD, "The key word is guts!\pThose here are training night and\nday to become BIRD {POKEMON}\lmasters. Come on!", "Gaaah!")
	msgbox("FALKNER's skills are for real!\nDon't get cocky just because you\lbeat me!", MSGBOX_DEFAULT)
	release
}

script VioletCity_Gym_EventScript_GymStatue {
    lockall
    if (flag(FLAG_BADGE01_GET))
    {
        msgbox("VIOLET CITY {POKEMON} GYM\n"
               "LEADER: FALKNER\p"
               "WINNING TRAINERS:\n"
               "{PLAYER}")
    }
    else
    {
        msgbox("VIOLET CITY {POKEMON} GYM")
    }
    releaseall
}

script VioletCity_Gym_EventScript_GymGuide {
	lock
	faceplayer
    if (flag(FLAG_DEFEATED_VIOLET_GYM))
    {
        msgbox("Nice battle! Keep it up, and you'll\n"
               "be the CHAMP in no time at all!")
    }
    else
    {
        msgbox("Hey! I'm no TRAINER, but I can\n"
               "give some advice!\p"
               "Believe me! If you believe, a\n"
               "championship dream can come true.\p"
               "You believe?\n"
               "Then listen.\p"
               "GRASS-type {POKEMON} are weak\n"
               "against FLYING-type {POKEMON}.\l"
               "Keep this in mind.")
    }
	release
}
