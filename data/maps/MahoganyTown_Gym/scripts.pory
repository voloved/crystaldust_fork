mapscripts MahoganyTown_Gym_MapScripts {}

script MahoganyTown_Gym_Eventscript_Pryce{
    lock
    faceplayer
    if(!flag(FLAG_BADGE07_GET))
    {
        msgbox("{POKEMON} have many experiences in\ntheir lives, just like we do.\pI, too, have seen and suffered\nmuch in my life.\pSince I am your elder, let me\nshow you what I mean.\pI have been with {POKEMON} since\nbefore you were born.\pI do not lose easily.\pI, PRYCE--the winter trainer--shall\ndemonstrate my power!")
        release
        trainerbattle_no_intro(TRAINER_PRYCE, "Ah, I am impressed by your prowess.\pWith your strong will, I know you\nwill overcome all life's obstacles.\pYou are worthy of this BADGE!")
        textcolor(MSG_COLOR_SYS)
        message("{PLAYER} received\nthe GLACIERBADGE.")
        call(Common_EventScript_PlayGymBadgeFanfare)
        textcolor(MSG_COLOR_PREV)
        setflag(FLAG_BADGE07_GET)
        setgymtrainers(7)
        callnative(CountBadges)
        if(var(VAR_RESULT) >= 6)
        {
            call(Common_EventScript_ActivateSixthBadgeRockets)
        }
    }
    if(!flag(FLAG_GOT_TM07_FROM_PRYCE))
    {
        msgbox("That BADGE will raise the SPECIAL\nstats of {POKEMON}.\pIt also lets your {POKEMON} use WHIRL-\nPOOL to get across real whirlpools.\pAnd this…\nThis is a gift from me!")
        checkitemspace(ITEM_TM07, 1)
        if(!var(VAR_RESULT))
        {
            goto(Common_EventScript_ShowBagIsFull)
        }
        giveitem(ITEM_TM07, 1)
        setflag(FLAG_GOT_TM07_FROM_PRYCE)
        msgbox("That TM contains HAIL.\pIt summons a hailstorm that lasts\nfor five turns.\pThe hailstorm damages all types\nexcept ICE.")
        release
        end
    }
    msgbox("When the ice and snow melt,\nspring arrives.\pYou and your {POKEMON} will be\ntogether for many years to come.\lCherish your time together!")
    release
    end
}

script MahoganyTown_Gym_Eventscript_Statues{
    if(flag(FLAG_BADGE07_GET))
    {
        msgbox("MAHOGANY TOWN {POKEMON} GYM\nLEADER: PRYCE\pWINNING TRAINERS:\n{PLAYER}", MSGBOX_SIGN)
        end
    }
    msgbox("MAHOGANY TOWN {POKEMON} GYM", MSGBOX_SIGN)
    end
}

script MahoganyTown_Gym_Eventscript_GymGuy{
    if(flag(FLAG_BADGE07_GET))
    {
        msgbox("PRYCE is something, but you're\nsomething else!\pThat was a hot battle that bridged\nthe generation gap!", MSGBOX_NPC)
        end
    }
    msgbox("PRYCE is a veteran who has trained\n{POKEMON} for some 50 years.\pHe's said to be good at freezing\nopponents with ICE-type moves.\pThat means you should melt him with\nyour burning ambition!", MSGBOX_NPC)
    end
}
// trainers from top to bottom
script MahoganyTown_Gym_Eventscript_BoarderDouglas{
    trainerbattle_single(TRAINER_DOUGLAS, "I know PRYCE's secret.", "Okay, I'll tell you PRYCE's secret.")
    msgbox("The secret behind PRYCE's power…\pHe meditates under a waterfall daily\nto strengthen his mind and body.", MSGBOX_AUTOCLOSE)
    end
}

script MahoganyTown_Gym_Eventscript_SkierRoxanne{
    trainerbattle_single(TRAINER_ROXANNE, "To get to PRYCE, our GYM LEADER,\nyou need to think before you skate.", "I wouldn't lose to you in skiing!")
    msgbox("If you don't skate with precision,\nyou won't get far in this GYM.", MSGBOX_AUTOCLOSE)
    end
}

script MahoganyTown_Gym_Eventscript_BoarderBrad{
    trainerbattle_single(TRAINER_BRAD, "This GYM has a slippery floor.\nIt's fun, isn't it?\pBut hey--we're not playing games\nhere!", "Do you see how serious we are?")
    msgbox("This GYM is great.\nI love boarding with my {POKEMON}!", MSGBOX_AUTOCLOSE)
    end
}

script MahoganyTown_Gym_Eventscript_BoarderRonald{ // bottom left corner
    trainerbattle_single(TRAINER_RONALD, "I'll freeze your {POKEMON}, so you\ncan't do a thing!", "Darn.\nI couldn't do a thing.")
    msgbox("I think there's a move a {POKEMON}\ncan use while it's frozen.", MSGBOX_AUTOCLOSE)
    end
}

script MahoganyTown_Gym_Eventscript_SkierClarissa{ // bottom right corner
    trainerbattle_single(TRAINER_CLARISSA, "Check out my parallel turn!", "No!\nYou made me wipe out!")
    msgbox("I shouldn't have been bragging\nabout my skiing…", MSGBOX_AUTOCLOSE)
    end
}