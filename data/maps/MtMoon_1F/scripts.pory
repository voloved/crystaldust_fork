mapscripts MtMoon_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION{
        setflag(FLAG_LANDMARK_MT_MOON)
        if(defeated(TRAINER_RIVAL_MT_MOON_CHIKORITA) || defeated(TRAINER_RIVAL_MT_MOON_CYNDAQUIL) || defeated(TRAINER_RIVAL_MT_MOON_TOTODILE))
        {
            setvar(VAR_TEMP_0, 1)
        }
        callnative(SetMtMoonRocksClearedGoneThroughLowerFloors)
        if(flag(FLAG_MT_MOON_ROCKS_CLEARED))
        {
            setobjectxyperm(4, 9, 21)
            setobjectmovementtype(4, MOVEMENT_TYPE_FACE_UP_AND_RIGHT)
        }
    }
    MAP_SCRIPT_ON_LOAD{
        if(!flag(FLAG_MT_MOON_ROCKS_CLEARED))
        {
            //leftmost boulder, bottom path
            setmetatile(9, 14, 0x2AA, 1)
            setmetatile(10, 14, 0x2AB, 1)
            setmetatile(9, 15, 0x2B2, 1)
            setmetatile(10, 15, 0x2B3, 1)
            //rightmost boulder, bottom path
            setmetatile(10, 16, 0x2AA, 1)
            setmetatile(11, 16, 0x2AB, 1)
            setmetatile(10, 17, 0x2B2, 1)
            setmetatile(11, 17, 0x2B3, 1)
            //top two combined rocks, bottom path 
            setmetatile(11, 14, 0x29F, 1)
            setmetatile(11, 15, 0x2A7, 1)
            //bottom-left two combined rocks, bottom path 
            setmetatile(9, 16, 0x29F, 1)
            setmetatile(9, 17, 0x2A7, 1)
            //bottom rubble pile, bottom path
            setmetatile(10, 18, 0x283, 1)
            //bottom rock, bottom path
            setmetatile(11, 18, 0x282, 1)
            //leftmost boulder, top path
            setmetatile(16, 5, 0x2AA, 1)
            setmetatile(17, 5, 0x2AB, 1)
            setmetatile(16, 6, 0x2B2, 1)
            setmetatile(17, 6, 0x2B3, 1)
            //rightmost boulder, top path
            setmetatile(18, 6, 0x2AA, 1)
            setmetatile(19, 6, 0x2AB, 1)
            setmetatile(18, 7, 0x2B2, 1)
            setmetatile(19, 7, 0x2B3, 1)
            //regular ground, top path (covering crater tile)
            setmetatile(16, 7, 0x281, 0)
            //regular ground, top path (replacing rock so there's room for Machoke)
            setmetatile(16, 8, 0x281, 0)
            //leftmost rock, top path
            setmetatile(17, 7, 0x282, 1)
            //topmost rubble pile, top path
            setmetatile(18, 5, 0x283, 1)
            //topmost rock, top path
            setmetatile(19, 5, 0x282, 1)
            //rightmost two combined rocks, top path
            setmetatile(20, 7, 0x29F, 1)
            setmetatile(20, 8, 0x2A7, 1)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_0, 0{
            lockall
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
            playse(SE_PIN)
            delay(0x40)
            setobjectxy(1, 12, 2)
            playse(SE_EXIT)
            playbgm(MUS_NONE, FALSE)
            waitse
            applymovement(1, MtMoon_1F_Movement_RivalToPlayer)
            waitmovement(0)
            playbgm(MUS_ENCOUNTER_RIVAL, 0)
            savebgm(MUS_RIVAL_EXIT)
            textcolor(0)
            msgbox("…… …… ……\nIt's been a while, {PLAYER}.\p…Since I lost to you, I thought\nabout what I was lacking with my\l{POKEMON}…\pAnd we came up with an answer.\n{PLAYER}, now we'll show you!", MSGBOX_DEFAULT)
            closemessage
            setvar(VAR_LAST_TALKED, 3)
            if (var(VAR_STARTER_MON) == STARTER_CHIKORITA)
            {
                trainerbattle_wintext(TRAINER_RIVAL_MT_MOON_CHIKORITA, 0, MtMoon_1F_Text_RivalLost, MtMoon_1F_Text_RivalWon)
            }
            elif (var(VAR_STARTER_MON) == STARTER_TOTODILE)
            {
                trainerbattle_wintext(TRAINER_RIVAL_MT_MOON_TOTODILE, 0, MtMoon_1F_Text_RivalLost, MtMoon_1F_Text_RivalWon)
            }
            elif (var(VAR_STARTER_MON) == STARTER_CYNDAQUIL)
            {
                trainerbattle_wintext(TRAINER_RIVAL_MT_MOON_CYNDAQUIL, 0, MtMoon_1F_Text_RivalLost, MtMoon_1F_Text_RivalWon)
            }
            msgbox("…… …… ……\n…You won, fair and square.\pI admit it.\nBut this isn't the end.\pI'm going to be the greatest\n{POKEMON} trainer ever.\pBecause these guys are behind me.\p…Listen, {PLAYER}.\pOne of these days I'm going to prove\nhow good I am by beating you.", MSGBOX_DEFAULT)
            closemessage
            applymovement(OBJ_EVENT_ID_PLAYER, MtMoon_1F_Movement_PlayerStepsRight)
            applymovement(1, MtMoon_1F_Movement_RivalExits)
            waitmovement(1)
            playse(SE_EXIT)
            settrainerflag(TRAINER_RIVAL_MT_MOON_CYNDAQUIL)
            settrainerflag(TRAINER_RIVAL_MT_MOON_TOTODILE)
            settrainerflag(TRAINER_RIVAL_MT_MOON_CHIKORITA)
            removeobject(1)
            setvar(VAR_TEMP_0, 1)
            fadedefaultbgm
            savebgm(MUS_RG_MT_MOON)
            releaseall
            end
        }
    ]
}movement(local) MtMoon_1F_Movement_RivalToPlayer{
    walk_down * 3
    walk_left * 4
    walk_down
    face_down
}text(local) MtMoon_1F_Text_RivalLost{
    "…… …… ……\pI thought I raised my {POKEMON} to be\nthe best they could be…\p…But it still wasn't enough…"
}text(local) MtMoon_1F_Text_RivalWon{
    "…… …… ……\pI've repaid my debt to you.\pWith my {POKEMON}, I'm going to beat\nthe CHAMPION and become the world's\lgreatest trainer."
}movement(local) MtMoon_1F_Movement_PlayerStepsRight{
    walk_right
    face_left
}movement(local) MtMoon_1F_Movement_RivalExits{
    delay_16
    walk_down
    set_invisible
}

script MtMoon_1F_EventScript_RockClearGuy1{
    if(flag(FLAG_MT_MOON_ROCKS_CLEARED))
    {
        msgbox("I don't think we'll see another\ncave-in for a long while.", MSGBOX_NPC)
        end
    }
    msgbox("MT. MOON still hasn't settled since\nit collapsed.\pCave-ins happen occasionally that\nhave to be cleared out by hand.\pIf you want to get to the other side,\nthe lower floors of the cave still\lhave a clear path.", MSGBOX_NPC)
    turnobject(VAR_LAST_TALKED, DIR_SOUTH)
    end
}

script MtMoon_1F_EventScript_RockClearGuy2{
    msgbox("This way leads to MT. MOON SQUARE.\n…But it's blocked by the cave-in…", MSGBOX_NPC)
    turnobject(VAR_LAST_TALKED, DIR_EAST)
    end
}

script MtMoon_1F_EventScript_Machoke1{
    call(GoldenrodCity_DeptStore_B1F_EventScript_Machoke)
    turnobject(VAR_LAST_TALKED, DIR_SOUTH)
}

script MtMoon_1F_EventScript_Machoke2{
    call(GoldenrodCity_DeptStore_B1F_EventScript_Machoke)
    turnobject(VAR_LAST_TALKED, DIR_EAST)
}