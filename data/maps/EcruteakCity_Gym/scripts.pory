mapscripts EcruteakCity_Gym_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0: EcruteakCity_Gym_Mapscript_OldMan
    ]
}
    
script EcruteakCity_Gym_Mapscript_OldMan{
    if(!flag(FLAG_AWAKENED_LEGENDARY_BEASTS))
    {
        applymovement(OBJ_EVENT_ID_PLAYER, EcruteakCity_Gym_Movement_PlayerUp)
        waitmovement(0)
        applymovement(6, EcruteakCity_Gym_Movement_OldManDown)
        waitmovement(0)
        textcolor(0)
        msgbox("MORTY, the GYM LEADER, is absent.\nSorry, but you'll have to leave.\pHohohoho.", MSGBOX_DEFAULT)
        closemessage
        setmetatile(7, 20, 0x357, 0) // a copy of the exit mat without the warp behavior to prevent the arrow from spawning.
        applymovement(6, EcruteakCity_Gym_Movement_OldManDown2)
        applymovement(OBJ_EVENT_ID_PLAYER, EcruteakCity_Gym_Movement_PlayerDown)
        waitmovement(0)
        setvar(VAR_TEMP_0, 1)
        warp(MAP_ECRUTEAK_CITY, 8, 0, 0)
        release
        end
    }
    setvar(VAR_TEMP_0, 1)
    end
}movement(local) EcruteakCity_Gym_Movement_PlayerUp{
    walk_up
}movement(local) EcruteakCity_Gym_Movement_OldManDown{
    walk_slow_down
}movement(local) EcruteakCity_Gym_Movement_OldManDown2{
    walk_down
}movement(local) EcruteakCity_Gym_Movement_PlayerDown{
    lock_facing_direction
    walk_slow_down
}

script EcruteakCity_Gym_Eventscript_Morty{
    trainerbattle_single(TRAINER_MORTY, "Good of you to have come.\pHere in ECRUTEAK, {POKEMON} have been\nrevered.\pIt's said that a rainbow-colored\n{POKEMON} will come down to appear\lbefore a truly powerful TRAINER.\pI believed that tale, so I have\nsecretly trained here all my life.\pAs a result, I can now see what\nothers cannot.\pJust a bit more…\pWith a little more, I could see a\nfuture in which I meet the {POKEMON}\lof rainbow colors.\pYou're going to help me reach that\nlevel!", "I'm not good enough yet…\nAll right. This BADGE is yours.", EcruteakCity_Gym_Eventscript_MortyDefeated, NO_MUSIC)
    if(!flag(FLAG_GOT_TM30_FROM_MORTY))
    {
        goto(EcruteakCity_Gym_Eventscript_GiveShadowBall)
    }
    msgbox("I see…\pYour journey has taken you to far-\naway places.\pAnd you have witnessed much more\nthan I.\pI envy you for that…")
    release
    end
}script(local) EcruteakCity_Gym_Eventscript_MortyDefeated{
    textcolor(MSG_COLOR_SYS)
    message("{PLAYER} received the FOGBADGE\nfrom MORTY.")
    call(Common_EventScript_PlayGymBadgeFanfare)
	textcolor(MSG_COLOR_PREV)
    setflag(FLAG_BADGE04_GET)
    setgymtrainers(4)
    //falls through to next script
}script(local) EcruteakCity_Gym_Eventscript_GiveShadowBall{
    msgbox("By having the FOGBADGE, {POKEMON} up\nto Lv50 will obey you.\pAlso, {POKEMON} that know SURF will be\nable to use that move anytime.\pI want you to have this too.")
    checkitemspace(ITEM_TM30, 1)
    if(!var(VAR_RESULT))
    {
        goto(Common_EventScript_ShowBagIsFull)
    }
    givetmhm_msg("{PLAYER} received TM30\nfrom MORTY.", ITEM_TM30)
    setflag(FLAG_GOT_TM30_FROM_MORTY)
    msgbox("It's SHADOW BALL. It causes damage\nand may reduce SP. DEF.\pUse it if it appeals to you.")
    release
    end
}

script EcruteakCity_Gym_Eventscript_Statues{
    if(flag(FLAG_BADGE04_GET))
    {
        msgbox("ECRUTEAK CITY {POKEMON} GYM\nLEADER: MORTY\pWINNING TRAINERS:\n{PLAYER}", MSGBOX_SIGN)
        end
    }
    msgbox("ECRUTEAK CITY {POKEMON} GYM", MSGBOX_SIGN)
    end
}

script EcruteakCity_Gym_Eventscript_GymGuy{
    if(flag(FLAG_BADGE04_GET))
    {
        msgbox("Whew, {PLAYER}.\nYou did great!\pI was cowering in the corner out of\npure terror!", MSGBOX_NPC)
        end
    }
    msgbox("The TRAINERS here have secret\nmotives.\pIf you win, they may tell you some\ndeep secrets about ECRUTEAK.", MSGBOX_NPC)
    end
}

script EcruteakCity_Gym_Eventscript_SagePing{ // lower Sage
    trainerbattle_single(TRAINER_PING, "Can you inflict any damage on our\n{POKEMON}?", "Ah! Well done!")
    msgbox("We use only GHOST-type {POKEMON}.\nNo NORMAL-type attack can harm them!", MSGBOX_AUTOCLOSE)
    end
}

script EcruteakCity_Gym_Eventscript_MediumGrace{ // lower Medium
    trainerbattle_single(TRAINER_GRACE, "Stumped by our invisible floor?\nDefeat me if you want a hint!", "Wha-what?")
    msgbox("Fine. I shall tell you the secret of\nthe invisible floor.\pThe path is right before our eyes!", MSGBOX_AUTOCLOSE)
    end
}

script EcruteakCity_Gym_Eventscript_SageJeffrey{ // upper Sage
    trainerbattle_single(TRAINER_JEFFREY, "I spent the spring with my {POKEMON}.\nThen summer, fall, and winter…\pThen spring came again. We have\nlived together for a long time.", "Wins and losses, I experienced both.")
    msgbox("Where did {POKEMON} come from?", MSGBOX_AUTOCLOSE)
    end
}

script EcruteakCity_Gym_Eventscript_MediumMartha{ // upper Medium
    trainerbattle_single(TRAINER_MARTHA, "I shall win!", "I, I, I lost!")
    msgbox("The one who wants to win most--will!", MSGBOX_AUTOCLOSE)
    end
}